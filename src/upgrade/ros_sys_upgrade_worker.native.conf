## Set Variables
:local UpdateChannel "stable";
:local RebootRequired false
## Set Variables End

## Upgrade Firmware
:log info ("Checking firmware status")
/system routerboard

:if ( [get current-firmware] != [get upgrade-firmware]) do={

    ## New version of firmware available, let's upgrade
    :log info ("Upgrading firmware of $[/system identity get name] from $[/system routerboard get current-firmware] to $[/system routerboard get upgrade-firmware]")
    ## Upgrade (it will no reboot, we'll do it later)
    upgrade
    :set RebootRequired true

} else={

    # No firmware upgrade available, just log info
    :log info ("Firmware of $[/system identity get name] is already up to date, good")

}
## Upgrade Firmware End

## Update RouterOS
:log info ("Checking RouterOS status")
/system package update
set channel=$UpdateChannel
check-for-updates

## Wait on slow connections
:delay 60s;

:if ([get installed-version] != [get latest-version]) do={

    ## Notify via Log
    :log info ("$[/system identity get name] can be updated from $[/system package update get installed-version] to $[/system package update get latest-version] ( channel: $[/system package update get channel] )")
    :log info ("Preparing to update RouterOS")
    ## Wait for system email log timer
    :delay 210s;
    :log info ("Start updating :) ")
    ## Wait for sometime & update
    :delay 90s;
    install

} else={

    :if ($RebootRequired) do={
        ## Firmware was upgraded, but not RouterOS, so we need to reboot to finish firmware upgrade
        :log info ("A firmware reboot is required")
        :delay 210s;
        :log info ("Start rebooting :) ")
        :delay 90s;
        /system reboot
    } else={
        ## No firmware nor RouterOS upgrade available, nothing to do, just log info
        :log info ("Current RouterOS version of $[/system identity get name] is $[/system package update get installed-version], good")
        :log info ("No newer RouterOS found. Just be relaxed, smile, and stay cool :) ")
    }

}
## Update RouterOS End

