## Set Variables
:local UpdateChannel "stable";
:local DeviceName [/system identity get name ];
:local RebootRequired false
## Set Variables End

## Upgrade Firmware
:log info ("Checking firmware status")

/system routerboard

:local SystemFirmware [/system routerboard get current-firmware ];
:local UpstreamFirmware [/system routerboard get upgrade-firmware ];

:if ( SystemFirmware != UpstreamFirmware ) do={

    ## New version of firmware available, let's upgrade
    :log info ("Upgrading firmware of $DeviceName from $SystemFirmware to $UpstreamFirmware")
    ## Upgrade (it will no reboot, we'll do it later)
    upgrade
    :set RebootRequired true

} else={

    # No firmware upgrade available, just log info
    :log info ("Firmware of $DeviceName is already up to date")

}
## Upgrade Firmware End

## Update RouterOS
:log info ("Checking RouterOS status")

/system package update
set channel=$UpdateChannel
check-for-updates

## Wait on slow connections
:delay 60s;

:local SystemVer [/system package update get installed-version ];
:local UpstreamVer [/system package update get latest-version ];
:local Status [/system package update get status ];
:local Channel [/system package update get channel ];

:if ( SystemVer != UpstreamVer ) do={

    ## Notify via Log
    :log info ("$DeviceName can be updated from $SystemVer to $UpstreamVer ( channel: $Channel )")
    :log info ("Preparing to update RouterOS")
    ## Wait for system email log timer
    :delay 210s;
    :log info ("Start updating :) ")
    ## Wait for sometime & update
    :delay 90s;
    install

} else={

    :if ( $RebootRequired ) do={

        ## Firmware was upgraded, but not RouterOS, so we need to reboot to finish firmware upgrade
        :log info ("A firmware reboot is required")
        :delay 210s;
        :log info ("Start rebooting :) ")
        :delay 90s;
        /system reboot

    } else={

        ## No firmware nor RouterOS upgrade available, nothing to do, just log info
        :log info ("Current RouterOS version of $DeviceName is $SystemVer")
        :log info ("Update check result: $Status")

    }

}
## Update RouterOS End

