## Set Variables
:local UpdateChannel "stable";
:local UpdateFirmware true;
:local DeviceName [/system identity get name ];
## Set Variables End

## Update RouterOS
:log info ("Checking RouterOS status ...")

/system package update
set channel=$UpdateChannel
check-for-updates

# Wait on slow connections
:delay 60s;

:local SystemVer [/system package update get installed-version ];
:local UpstreamVer [/system package update get latest-version ];
:local Status [/system package update get status ];
:local Channel [/system package update get channel ];

:if ($SystemVer != $UpstreamVer) do={
    # New version of RouterOS available
    :log info ("$DeviceName can be updated from $SystemVer to $UpstreamVer ( channel: $Channel )")
    :log info ("Preparing to update RouterOS")
    # Wait for system email log timer
    :delay 210s;
    :log info ("Start updating :) ")
    :delay 90s;
    install
} else={
    # No RouterOS update available, nothing to do, just log info
    :log info ("RouterOS of $DeviceName is $SystemVer")
    :log info ("$Status")
}
## Update RouterOS End

## Upgrade Firmware
:if ($UpdateFirmware) do={
    :log info ("Checking Firmware status ...")
    /system routerboard
    :local SystemFirmware [/system routerboard get current-firmware ];
    :local UpstreamFirmware [/system routerboard get upgrade-firmware ];

    :if ($SystemFirmware != $UpstreamFirmware) do={
        # New version of firmware available
        :log info ("Upgrading firmware of $DeviceName from $SystemFirmware to $UpstreamFirmware")
        upgrade
        # Firmware upgraded, need to reboot to finish
        :log info ("A firmware reboot is required")
        :delay 210s;
        :log info ("Start rebooting :) ")
        :delay 90s;
        /system reboot
    } else={
        # No firmware upgrade available, log info
        :log info ("Firmware of $DeviceName is $SystemFirmware")
        :log info ("Firmware is already up to date")
    }

} else={
    # Automatic firmware upgrade disabled, log info
    :log info ("Firmware automatic upgrades disabled")
}
## Upgrade Firmware End

