## Fox RouterOS Notification
:local Project "RESOURCES REPORT";
:local WanInterface "pppoe-out1";                   # Need to customize
:local IPv6Interface "bridge1";                     # Need to customize
:local updChannel "stable";

## Check For Upgrade
:local SystemVer [/system package update get installed-version ];
:local UpstreamVer;
:local Status;
:local Channel;

:if ($UpstreamVer = nothing) do={
    /system package update set channel=$updChannel
    /system package update check-for-updates
    :delay 10s;
    :set UpstreamVer [/system package update get latest-version ];
    :set Status [/system package update get status ];
    :set Channel [/system package update get channel ];
}
## Check For Upgrade End

## Check Licence Status
:local Level [/system license get level ];
:local Renewal [/system license get next-renewal-at ];
:local Deadline [/system license get deadline-at ];
## Check Licence Status End

## Check CPU Status
:local CpuCores [/system resource get cpu-count ];
:local CpuLoad [/system resource get cpu-load ];
:local CpuFrequency [/system resource get cpu-frequency ];
## Check CPU Status End

## Check Memory Status
:local MemoryFree [/system resource get free-memory ];
:local MemoryTotal [/system resource get total-memory ];
## Check Memory Status End

## Check Disk Status
:local DiskFree [/system resource get free-hdd-space ];
:local DiskTotal [/system resource get total-hdd-space ];
:local DiskWrites [/system resource get write-sect-since-reboot ];
:local DiskWritesTotal [/system resource get write-sect-total ];
## Check Disk Status End

## Check WAN Status
:local WanIPv4 [/ip address get [/ip address find interface=$WanInterface] address ];
:local WanIPv4Network [/ip address get [/ip address find interface=$WanInterface] network ];
:local WanLinkDownTime [/interface get $WanInterface last-link-down-time ];
:local WanLinkUpTime [/interface get $WanInterface last-link-up-time ];
:local WanLinkDowns [/interface get $WanInterface link-downs ];
:local WanTx [/interface get $WanInterface tx-byte ];
:local WanRx [/interface get $WanInterface rx-byte ];
:local WanTxDrop [/interface get $WanInterface tx-drop ];
:local WanRxDrop [/interface get $WanInterface rx-drop ];
:local WanTxError [/interface get $WanInterface tx-error ];
:local WanRxError [/interface get $WanInterface rx-error ];
## Check WAN Status End

## Check WAN IPv6 Status
:local WanIPv6;
:local WanIPv6Prefix;
:local WanIPv6PrefixLength;

:local FindInterface [/ipv6 address find where interface=$IPv6Interface advertise=yes ];
:local LenInterface [:len $FindInterface];
:local IPv6Probe ($FindInterface -> ($LenInterface-2));

:if ($IPv6Probe != nothing) do={
    # Check IPv6 Status Passed
    :set WanIPv6 [/ipv6 address get $IPv6Probe address ];
    :set WanIPv6Prefix [/ipv6 dhcp-client get $WanInterface prefix ];
    :set WanIPv6PrefixLength [/ipv6 dhcp-client get $WanInterface pool-prefix-length ];
} else={
    # Check IPv6 Status Failed
    :set WanIPv6 "N/A";
    :set WanIPv6Prefix "N/A";
    :set WanIPv6PrefixLength "N/A";
}
## Check WAN IPv6 Status End

## Resolve Mail Server
:local MailServer "<smtp.domain_a.com>";            # Need to customize
:local MailPort "465";
:local MailTls "yes";
:local SendFrom "<your_email_a@domain_a.com>";      # Need to customize
:local SendTo "<your_email_b@domain_b.com>";        # Need to customize
:local MailIp;
:set MailIp [:resolve $MailServer ];
:delay 1s;
## Resolve End

:local DeviceName [/system identity get name ];
:local Time [/system clock get time ];
:local Date [/system clock get date ];
:local Uptime [/system resource get uptime ];

/tool e-mail send server=$MailIp port=$MailPort tls=$MailTls from=$SendFrom to=$SendTo \
subject="$DeviceName $Project: [Uptime: $Uptime] [$Date $Time]" \
body="
====== RouterOS Daily Report ====== \n

Router Name: $DeviceName \n
System Date: $Date $Time \n
System Uptime: $Uptime \n

Current version is: $SystemVer \n
Upstream version is: $UpstreamVer ( channel: $Channel ) \n
System Status: $Status \n

System Licence Level: $Level \n
Next Renewal At: $Renewal \n
Deadline At: $Deadline \n

====== WAN STATUS ====== \n

Interface: $WanInterface \n

Last Link Down Time: $WanLinkDownTime \n
Last Link Up Time: $WanLinkUpTime \n
Link Downs: $WanLinkDowns \n

IPv4 Address: $WanIPv4 \n
Networek: $WanIPv4Network \n

IPv6 Address: $WanIPv6 \n
Prefix: $WanIPv6Prefix \n
Pool Prefix Length: $WanIPv6PrefixLength \n

WAN Tx: $[($WanTx / 1048576)] MiB ( as $[($WanTx / 1073741824)] GiB ) \n
WAN Rx: $[($WanRx / 1048576)] MiB ( as $[($WanRx / 1073741824)] GiB ) \n

WAN Tx Quality -- Drop: $WanTxDrop  Error: $WanTxError \n
WAN Rx Quality -- Drop: $WanRxDrop  Error: $WanRxError \n

====== CPU ====== \n

Core: $CpuCores Cores \n
Load: $CpuLoad % \n
Frequency: $CpuFrequency MHz \n

====== Memory ====== \n

Free:  $[($MemoryFree / 1048576)] MiB \n
Total: $[($MemoryTotal / 1048576)] MiB \n
Used:  $[(($MemoryTotal / 1048576) - ($MemoryFree / 1048576))] MiB \n

====== Disk ====== \n

Free:  $[($DiskFree / 1048576)] MiB \n
Total: $[($DiskTotal / 1048576)] MiB \n
Used:  $[(($DiskTotal / 1048576) - ($DiskFree / 1048576))] MiB \n

Sector Writes Since Reboot: $DiskWrites \n
Total Sector Writes: $DiskWritesTotal \n
"
## Fox RouterOS Notification End

