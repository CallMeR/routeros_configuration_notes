## 说明
# 1.将 PPPoE 拨号的账户根据实际情况修改，并设置密码。
# 2.将内网网段 172.16.1.0 和 RouterOS IP 地址 172.16.1.1/24 根据实际情况修改。
# 3.将光猫网段 192.168.100.0 接口 ether1 IP 地址 192.168.100.2/24 根据实际情况修改。
# 4.内网没有 DNS 服务器 172.16.1.2/24 和 172.16.1.3/24 时，DHCP 和 DNS 设置需要根据实际情况修改。
# 5.DHCP Options 需要根据实际情况修改。
# 6.新系统管理员账号 用户名、密码 需要修改，账户添加完成后，用新管理员账户执行后续命令条目。
# 7.防火墙现在已经默认启用 fasttrack-connection，QoS 默认使用 Queue Tree。
# 8.Qos 的带宽限速需要根据签约带宽进行修改。
# 9.系统日志邮件的 发件箱、收件箱、SMTP密码 需要根据实际情况修改。
# 10.DHCP 中 MAC 地址绑定静态 IP ，具体参数需要根据实际情况修改。
# 11.将内网 IPv6 地址 fdac::/64 换成合法的 ULA 地址。
# 12.内网没有 IPv6 DNS 服务器 fdac::2 和 fdac::3 时，IPv6 DNS 设置需要根据实际情况修改。
# 13.硬盘格式化过程中会忽略后续命令，需要等待格式化完成后再执行后续命令条目。

## 执行第一部分
## 配置网口
/interface ethernet
set [ find default-name=ether1 ] comment="defconf: Local WAN" disable-running-check=no
set [ find default-name=ether2 ] comment="defconf: Local LAN1" disable-running-check=no
set [ find default-name=ether3 ] comment="defconf: Local LAN2" disable-running-check=no
set [ find default-name=ether4 ] comment="defconf: Local LAN3" disable-running-check=no
set [ find default-name=ether5 ] comment="defconf: Local LAN for VMs" disable-running-check=no

/interface bridge
add auto-mac=yes comment="defconf: Local Bridge" name=bridge1

/interface bridge port
add bridge=bridge1 interface=ether2
add bridge=bridge1 interface=ether3
add bridge=bridge1 interface=ether4
add bridge=bridge1 interface=ether5
## 第一部分完毕

## 执行第二部分
## PPPoE & DNS & DHCP
/interface pppoe-client
add add-default-route=yes use-peer-dns=no comment="defconf: Local PPPoE IPv4" disabled=yes interface=ether1 name=pppoe-out1 user="<your_pppoe_user_name>" password="<your_pppoe_user_password>"

/ip address
add address=172.16.1.1/24 comment="defconf: Local LAN IPv4 Address" interface=bridge1 network=172.16.1.0
add address=192.168.100.2/24 comment="onuconf: Link IPv4 Address For ONU" interface=ether1 network=192.168.100.0

/ip pool
add comment="defconf: Local LAN IPv4 Pool" name=Local_Pool_IPv4 ranges=172.16.1.101-172.16.1.150

/ip dhcp-server
add address-pool=Local_Pool_IPv4 bootp-support=none comment="defconf: Local LAN IPv4 DHCP Server" interface=bridge1 lease-time=1d name=Local_DHCP_IPv4

/ip dhcp-server network
add address=172.16.1.0/24 comment="defconf: Local LAN IPv4 DHCP Network" dns-server=172.16.1.2,172.16.1.3 domain=fox.local gateway=172.16.1.1 netmask=24

/ip dns
set allow-remote-requests=yes cache-max-ttl=6h cache-size=1024KiB max-concurrent-queries=150 servers=172.16.1.2,172.16.1.3

/ip dhcp-server option
add code=6 name=Opt_DNS_HA value="'172.16.1.1'"
add code=3 name=Opt_GW value="'172.16.1.50'"
add code=6 name=Opt_DNS value="'172.16.1.50'"

/ip dhcp-server option sets
add name=Opt_Bypass options=Opt_DNS,Opt_GW

/ip dhcp-client
remove numbers=[find where interface ~ "ether1"]
## 第二部分完毕

## 执行第三部分
## 系统参数调整
/system identity
set name=FoxRouter

/ipv6 settings
set disable-ipv6=yes

/system clock
set time-zone-name=Asia/Shanghai

/system ntp client
set enabled=yes

/system ntp client servers
add address=ntp.aliyun.com
add address=ntp.tencent.com

/ip service
set telnet address=172.16.1.0/24 disabled=yes
set ftp address=172.16.1.0/24 disabled=yes
set www address=172.16.1.0/24
set ssh address=172.16.1.0/24
set www-ssl address=172.16.1.0/24
set api address=172.16.1.0/24 disabled=yes
set winbox address=172.16.1.0/24
set api-ssl address=172.16.1.0/24 disabled=yes

/user group
set read policy=read,winbox,web,!local,!telnet,!ssh,!ftp,!reboot,!write,!policy,!test,!password,!sniff,!sensitive,!api,!romon,!rest-api

/user
add name="<your_ros_user_name>" password="<your_ros_user_password>" group=full address=172.16.1.0/24 comment="defconf: System Admin User"
set admin group=read address=172.16.1.0/24 comment="defconf: System Default User"
## 第三部分完毕

## 执行第四部分
## 防火墙 & QoS
## Filter规则16条+虚拟规则1条
## NAT规则6条
## Mangle规则2条+虚拟规则3条
## Raw规则46条+虚拟规则1条
## Address-list规则30条
/interface list
add name=WAN comment="defconf: Connect To Global"
add name=LAN comment="defconf: Local Bridge"
add name=ONU comment="onuconf: Access To ONU"

/interface list member
add interface=pppoe-out1 list=WAN comment="defconf: Connect To Global"
add interface=bridge1 list=LAN comment="defconf: Local Bridge"
add interface=ether1 list=ONU comment="onuconf: Access To ONU"


/ip firewall address-list

add address=192.168.100.1 comment="onuconf: ONU Address" list=onu_ipv4
add address=172.16.1.0/24 comment="lanconf: Local Address" list=local_subnet_ipv4

add address=172.16.1.1 comment="lanconf: Local DNS Address" list=local_dns_ipv4
add address=172.16.1.2 comment="lanconf: Local DNS Address" list=local_dns_ipv4
add address=172.16.1.3 comment="lanconf: Local DNS Address" list=local_dns_ipv4

add address=0.0.0.0/8 comment="defconf: RFC6890" list=no_forward_ipv4
add address=169.254.0.0/16 comment="defconf: RFC6890" list=no_forward_ipv4
add address=224.0.0.0/4 comment="defconf: multicast" list=no_forward_ipv4
add address=255.255.255.255/32 comment="defconf: RFC6890" list=no_forward_ipv4

add address=127.0.0.0/8 comment="defconf: RFC6890" list=bad_ipv4
add address=192.0.0.0/24 comment="defconf: RFC6890" list=bad_ipv4
add address=192.0.2.0/24 comment="defconf: RFC6890 documentation" list=bad_ipv4
add address=198.51.100.0/24 comment="defconf: RFC6890 documentation" list=bad_ipv4
add address=203.0.113.0/24 comment="defconf: RFC6890 documentation" list=bad_ipv4
add address=240.0.0.0/4 comment="defconf: RFC6890 reserved" list=bad_ipv4

add address=0.0.0.0/8 comment="defconf: RFC6890" list=not_global_ipv4
add address=10.0.0.0/8 comment="defconf: RFC6890" list=not_global_ipv4
add address=100.64.0.0/10 comment="defconf: RFC6890" list=not_global_ipv4
add address=169.254.0.0/16 comment="defconf: RFC6890" list=not_global_ipv4
add address=172.16.0.0/12 comment="defconf: RFC6890" list=not_global_ipv4
add address=192.0.0.0/29 comment="defconf: RFC6890" list=not_global_ipv4
add address=192.168.0.0/16 comment="defconf: RFC6890" list=not_global_ipv4
add address=198.18.0.0/15 comment="defconf: RFC6890 benchmark" list=not_global_ipv4
add address=255.255.255.255/32 comment="defconf: RFC6890" list=not_global_ipv4

add address=224.0.0.0/4 comment="defconf: multicast" list=bad_src_ipv4
add address=255.255.255.255/32 comment="defconf: RFC6890" list=bad_src_ipv4

add address=0.0.0.0/8 comment="defconf: RFC6890" list=bad_dst_ipv4
add address=224.0.0.0/4 comment="defconf: RFC6890" list=bad_dst_ipv4

add comment="ddosconf: DDoS" list=ddos_targets_ipv4
add comment="ddosconf: DDoS" list=ddos_attackers_ipv4


/ip firewall filter

add action=accept chain=input comment="defconf: accept ICMP after RAW" protocol=icmp
add action=accept chain=input comment="defconf: accept established,related" connection-state=established,related
add action=drop chain=input comment="defconf: drop invalid" connection-state=invalid
add action=drop chain=input comment="defconf: drop all not coming from LAN" in-interface-list=!LAN

add action=fasttrack-connection chain=forward comment="defconf: fasttrack" connection-state=established,related disabled=no
add action=accept chain=forward comment="defconf: accept established,related" connection-state=established,related
add action=drop chain=forward comment="defconf: drop invalid" connection-state=invalid
add action=drop chain=forward comment="defconf: drop all from WAN not DSTNATed" connection-nat-state=!dstnat connection-state=new in-interface-list=WAN log=yes log-prefix=fw_wan_not_DSTNATed
add action=drop chain=forward comment="onuconf: drop all from ONU not DSTNATed" connection-nat-state=!dstnat connection-state=new in-interface-list=ONU log=yes log-prefix=fw_onu_not_DSTNATed
add action=drop chain=forward src-address-list=no_forward_ipv4 comment="defconf: drop bad forward IPs"
add action=drop chain=forward dst-address-list=no_forward_ipv4 comment="defconf: drop bad forward IPs"

add action=jump chain=forward connection-state=new jump-target=detect_ddos comment="ddosconf: DDoS"
add action=return chain=detect_ddos dst-limit=32,32,src-and-dst-addresses/10s protocol=tcp tcp-flags=syn,ack comment="ddosconf: SYN-ACK Flood" log=yes log-prefix=fw_syn_ack_detected
add action=return chain=detect_ddos dst-limit=96,64,src-and-dst-addresses/10s comment="ddosconf: DDoS"
add action=add-dst-to-address-list chain=detect_ddos address-list=ddos_targets_ipv4 address-list-timeout=10m comment="ddosconf: DDoS"
add action=add-src-to-address-list chain=detect_ddos address-list=ddos_attackers_ipv4 address-list-timeout=10m comment="ddosconf: DDoS" log=yes log-prefix=fw_ddos_attackers


/ip firewall nat

add action=masquerade chain=srcnat comment="defconf: masquerade" out-interface-list=WAN
add action=masquerade chain=srcnat out-interface-list=ONU src-address-list=local_subnet_ipv4 dst-address-list=onu_ipv4 comment="onuconf: Access To ONU"

add action=accept chain=dstnat dst-port=53 in-interface-list=LAN protocol=udp src-address-list=local_dns_ipv4 comment="lanconf: Accept Local DNS Startup Query (UDP)"
add action=accept chain=dstnat dst-port=53 in-interface-list=LAN protocol=tcp src-address-list=local_dns_ipv4 comment="lanconf: Accept Local DNS Startup Query (TCP)" log=yes log-prefix=fw_dnsv4_tcp
add action=redirect chain=dstnat dst-port=53 in-interface-list=LAN protocol=udp to-ports=53 comment="lanconf: DNS Redirect to Local (UDP)"
add action=redirect chain=dstnat dst-port=53 in-interface-list=LAN protocol=tcp to-ports=53 comment="lanconf: DNS Redirect to Local (TCP)" log=yes log-prefix=fw_dnsv4_tcp


/ip firewall mangle

add action=change-mss chain=forward comment="defconf: Fix IPv4 MSS For WAN" new-mss=clamp-to-pmtu passthrough=yes protocol=tcp tcp-flags=syn
add action=accept chain=prerouting src-address-list=local_subnet_ipv4 dst-address-list=onu_ipv4 comment="onuconf: Access To ONU"


/ip firewall raw

add action=accept chain=prerouting comment="defconf: enable for transparent firewall" disabled=yes

add action=drop chain=prerouting dst-address-list=ddos_targets_ipv4 src-address-list=ddos_attackers_ipv4 comment="ddosconf: DDoS"

add action=accept chain=prerouting comment="defconf: accept DHCP discover" dst-address=255.255.255.255 dst-port=67 in-interface-list=LAN protocol=udp src-address=0.0.0.0 src-port=68

add action=drop chain=prerouting comment="defconf: drop bogon IP's" src-address-list=bad_ipv4
add action=drop chain=prerouting comment="defconf: drop bogon IP's" dst-address-list=bad_ipv4
add action=drop chain=prerouting comment="defconf: drop bogon IP's" src-address-list=bad_src_ipv4
add action=drop chain=prerouting comment="defconf: drop bogon IP's" dst-address-list=bad_dst_ipv4

add action=drop chain=prerouting comment="defconf: drop non global from WAN" src-address-list=not_global_ipv4 in-interface-list=WAN
add action=drop chain=prerouting comment="defconf: drop forward to local lan from WAN" in-interface-list=WAN dst-address-list=local_subnet_ipv4 log=yes log-prefix=fw_wan_to_lan_forward

add action=drop chain=prerouting comment="onuconf: drop if not from ONU IP" in-interface-list=ONU src-address-list=!onu_ipv4 log=yes log-prefix=fw_not_from_onu_address
add action=drop chain=prerouting comment="onuconf: drop forward to local lan from ONU" in-interface-list=ONU dst-address-list=local_subnet_ipv4 log=yes log-prefix=fw_onu_to_lan_forward

add action=drop chain=prerouting comment="defconf: drop local if not from default IP range" in-interface-list=LAN src-address-list=!local_subnet_ipv4

add action=drop chain=prerouting comment="defconf: drop bad UDP" port=0 protocol=udp log=yes log-prefix=fw_udp_0_port

add action=jump chain=prerouting comment="defconf: jump to ICMP chain" jump-target=icmp4 protocol=icmp
add action=jump chain=prerouting comment="defconf: jump to TCP chain" jump-target=bad_tcp protocol=tcp

add action=accept chain=prerouting comment="defconf: accept everything else from LAN" in-interface-list=LAN
add action=accept chain=prerouting comment="defconf: accept everything else from WAN" in-interface-list=WAN
add action=accept chain=prerouting comment="onuconf: accept everything else from ONU" in-interface-list=ONU

add action=drop chain=prerouting comment="defconf: drop the rest"

add action=drop chain=bad_tcp comment="defconf: TCP flag filter" protocol=tcp tcp-flags=!fin,!syn,!rst,!ack
add action=drop chain=bad_tcp comment="defconf: tcp-flags=fin,syn" protocol=tcp tcp-flags=fin,syn
add action=drop chain=bad_tcp comment="defconf: tcp-flags=fin,rst" protocol=tcp tcp-flags=fin,rst
add action=drop chain=bad_tcp comment="defconf: tcp-flags=fin,!ack" protocol=tcp tcp-flags=fin,!ack
add action=drop chain=bad_tcp comment="defconf: tcp-flags=fin,urg" protocol=tcp tcp-flags=fin,urg
add action=drop chain=bad_tcp comment="defconf: tcp-flags=syn,rst" protocol=tcp tcp-flags=syn,rst
add action=drop chain=bad_tcp comment="defconf: tcp-flags=rst,urg" protocol=tcp tcp-flags=rst,urg
add action=drop chain=bad_tcp comment="defconf: TCP port 0 drop" port=0 protocol=tcp log=yes log-prefix=fw_tcp_0_port

add action=accept chain=icmp4 comment="lanconf: Accept echo reply from WAN" icmp-options=0:0 limit=10,10:packet protocol=icmp in-interface-list=WAN
add action=accept chain=icmp4 comment="lanconf: Accept net unreachable from WAN" icmp-options=3:0 protocol=icmp in-interface-list=WAN
add action=accept chain=icmp4 comment="lanconf: Accept fragmentation needed from WAN" icmp-options=3:4 protocol=icmp in-interface-list=WAN
add action=accept chain=icmp4 comment="lanconf: Accept time exceeded from WAN" icmp-options=11:0-255 protocol=icmp in-interface-list=WAN
add action=drop chain=icmp4 comment="lanconf: Drop other icmp from WAN" protocol=icmp in-interface-list=WAN

add action=accept chain=icmp4 comment="onuconf: Accept echo reply from ONU" icmp-options=0:0 limit=10,10:packet protocol=icmp in-interface-list=ONU
add action=drop chain=icmp4 comment="onuconf: Drop other icmp from ONU" protocol=icmp in-interface-list=ONU log=yes log-prefix=fw_drop_onu_icmp

add action=accept chain=icmp4 comment="defconf: echo reply" icmp-options=0:0 limit=10,10:packet protocol=icmp in-interface-list=LAN log=yes log-prefix=fw_lan_ehco_reply
add action=accept chain=icmp4 comment="defconf: net unreachable" icmp-options=3:0 protocol=icmp in-interface-list=LAN
add action=accept chain=icmp4 comment="defconf: host unreachable" icmp-options=3:1 protocol=icmp in-interface-list=LAN
add action=accept chain=icmp4 comment="defconf: protocol unreachable" icmp-options=3:2 protocol=icmp in-interface-list=LAN
add action=accept chain=icmp4 comment="defconf: port unreachable" icmp-options=3:3 protocol=icmp in-interface-list=LAN
add action=accept chain=icmp4 comment="defconf: fragmentation needed" icmp-options=3:4 protocol=icmp in-interface-list=LAN

add action=accept chain=icmp4 comment="defconf: echo to ONU" icmp-options=8:0 limit=10,10:packet protocol=icmp in-interface-list=LAN dst-address-list=onu_ipv4
add action=accept chain=icmp4 comment="defconf: echo to Local Device" icmp-options=8:0 limit=10,10:packet protocol=icmp in-interface-list=LAN dst-address-list=local_subnet_ipv4
add action=drop chain=icmp4 comment="defconf: echo to Not Global" icmp-options=8:0 limit=10,10:packet protocol=icmp in-interface-list=LAN dst-address-list=not_global_ipv4
add action=accept chain=icmp4 comment="defconf: echo to WAN" icmp-options=8:0 limit=10,10:packet protocol=icmp in-interface-list=LAN
add action=accept chain=icmp4 comment="defconf: time exceeded" icmp-options=11:0-255 protocol=icmp in-interface-list=LAN
add action=drop chain=icmp4 comment="defconf: drop all other icmp" protocol=icmp


/ip settings
set tcp-syncookies=yes max-neighbor-entries=1024

/ip neighbor discovery-settings
set discover-interface-list=none

/ip proxy
set enabled=no

/ip socks
set enabled=no

/ip upnp
set enabled=no

/ip cloud
set ddns-enabled=no update-time=no

/ip ssh
set strong-crypto=yes

/tool mac-server
set allowed-interface-list=none

/tool mac-server mac-winbox
set allowed-interface-list=none

/tool mac-server ping
set enabled=no

/tool bandwidth-server
set enabled=no

/queue type
add cake-diffserv=diffserv4 cake-flowmode=triple-isolate cake-memlimit=32.0MiB cake-rtt=60ms cake-overhead-scheme=ethernet cake-nat=no kind=cake name=cake_rx
add cake-diffserv=diffserv4 cake-flowmode=triple-isolate cake-memlimit=32.0MiB cake-rtt=60ms cake-overhead-scheme=ethernet cake-nat=yes kind=cake cake-ack-filter=filter name=cake_tx

/queue tree
add comment="qosconf: download queue with cake" bucket-size=0.05 max-limit=500M name=cake_download packet-mark=no-mark parent=bridge1 queue=cake_rx
add comment="qosconf: upload queue with cake" bucket-size=0.03 max-limit=50M name=cake_upload packet-mark=no-mark parent=pppoe-out1 queue=cake_tx
## 第四部分完毕

## 执行第五部分
## 定时任务
/tool/e-mail/set address="<smtp.domain_a.com>" from="<your_email_a@domain_a.com>" port=465 tls=yes user="<your_email_a@domain_a.com>" password="<your_smtp_password>"

/system scheduler

add comment="sysconf: system alert log email" interval=45m name=system_log_timer on-event="/system script run email_log_worker" policy=read,write,policy,test start-time=00:00:00
add comment="sysconf: system resource email" interval=6h name=system_res_timer on-event="/system script run email_res_worker" policy=read,write,policy,test start-time=00:05:00

add comment="sysconf: system os auto upgrade" interval=1d name=system_upgrade_timer on-event="/system script run sys_upgrade_worker" policy=reboot,read,write,policy,password start-time=02:55:00

add comment="pppoeconf: system disable PPPoE" interval=3d name=disable_pppoe_timer on-event="/interface disable pppoe-out1" policy=write start-time=04:00:00
add comment="pppoeconf: system enable PPPoE" interval=3d name=enable_pppoe_timer on-event="/interface enable pppoe-out1" policy=write start-time=04:00:10

/system script

add comment="sysconf: system alert log email" dont-require-permissions=no name=email_log_worker policy=read,write,policy,test source=""
add comment="sysconf: system resource email" dont-require-permissions=no name=email_res_worker policy=read,write,policy,test source=""

add comment="sysconf: system os auto upgrade" dont-require-permissions=no name=sys_upgrade_worker policy=reboot,read,write,policy,password source=""
## 第五部分完毕

## 执行第六部分
## 静态IP
/ip dhcp-server lease

add address=172.16.1.10 comment="<Your_Device_Name1>" lease-time=2d mac-address=AA:BB:CC:00:00:10 server=Local_DHCP_IPv4
add address=172.16.1.20 comment="<Your_Device_Name2>" dhcp-option=Opt_DNS_HA lease-time=2d mac-address=AA:BB:CC:00:00:20 server=Local_DHCP_IPv4
add address=172.16.1.30 comment="<Your_Device_Name3>" dhcp-option-set=Opt_Bypass lease-time=2d mac-address=AA:BB:CC:00:00:30 server=Local_DHCP_IPv4
## 第六部分完毕

## 执行第七部分
## 设置IPv6
## Filter规则26条
## NAT规则5条
## Mangle规则1条
## Raw规则47条
## Address-list规则20条
/ipv6 settings
set disable-ipv6=no max-neighbor-entries=1024

/ip dns
set allow-remote-requests=yes cache-max-ttl=6h cache-size=1024KiB max-concurrent-queries=150 servers=172.16.1.2,172.16.1.3,fdac::2,fdac::3

/ipv6 dhcp-client
add add-default-route=no comment="defconf: DHCPv6 on PPPoE" interface=pppoe-out1 pool-name=Local_Pool_GUA_IPv6 request=prefix use-peer-dns=no

/ipv6 address
add address=::1 advertise=yes comment="defconf: Local LAN GUA IPv6 Address" from-pool=Local_Pool_GUA_IPv6 interface=bridge1
add address=fdac::1 advertise=yes comment="defconf: Local LAN ULA IPv6 Address" interface=bridge1

/ipv6 nd prefix default
set preferred-lifetime=1h valid-lifetime=2h

/ipv6 nd
set [ find default=yes ] disabled=yes
add interface=bridge1 ra-interval=1m-2m hop-limit=64 dns=fdac::2,fdac::3 advertise-mac-address=yes advertise-dns=yes


/ipv6 firewall address-list

add address=fdac::1 comment="lanconf: Local DNS Address" list=local_dns_ipv6
add address=fdac::2 comment="lanconf: Local DNS Address" list=local_dns_ipv6
add address=fdac::3 comment="lanconf: Local DNS Address" list=local_dns_ipv6

add address=fe80::/10 comment="defconf: RFC6890 Linked-Scoped Unicast" list=no_forward_ipv6
add address=ff00::/8 comment="defconf: multicast" list=no_forward_ipv6

add address=::1/128 comment="defconf: RFC6890 lo" list=bad_ipv6
add address=::ffff:0:0/96 comment="defconf: RFC6890 IPv4 mapped" list=bad_ipv6
add address=2001::/23 comment="defconf: RFC6890" list=bad_ipv6
add address=2001:db8::/32 comment="defconf: RFC6890 documentation" list=bad_ipv6
add address=2001:10::/28 comment="defconf: RFC6890 orchid" list=bad_ipv6
add address=::/96 comment="defconf: ipv4 compat" list=bad_ipv6

add address=100::/64 comment="defconf: RFC6890 Discard-only" list=not_global_ipv6
add address=2001::/32 comment="defconf: RFC6890 TEREDO" list=not_global_ipv6
add address=2001:2::/48 comment="defconf: RFC6890 Benchmark" list=not_global_ipv6
add address=fc00::/7 comment="defconf: RFC6890 Unique-Local" list=not_global_ipv6

add address=::/128 comment="defconf: unspecified" list=bad_dst_ipv6

add address=::/128 comment="defconf: unspecified" list=bad_src_ipv6
add address=ff00::/8 comment="defconf: multicast" list=bad_src_ipv6

add address=::/128 comment="ddosconf: DDoS" list=ddos_targets_ipv6
add address=::/128 comment="ddosconf: DDoS" list=ddos_attackers_ipv6


/ipv6 firewall filter

add action=accept chain=input comment="defconf: accept ICMPv6 after RAW" protocol=icmpv6
add action=accept chain=input comment="defconf: accept established,related" connection-state=established,related
add action=drop chain=input comment="defconf: drop invalid" connection-state=invalid
add action=accept chain=input comment="defconf: accept UDP traceroute" port=33434-33534 protocol=udp
add action=accept chain=input comment="defconf: accept DHCPv6-Client prefix delegation." dst-port=546 protocol=udp src-address=fe80::/16 log=yes log-prefix=fw_ipv6_pd
add action=accept chain=input comment="defconf: accept IKE" dst-port=500,4500 protocol=udp
add action=accept chain=input comment="defconf: accept IPSec AH" protocol=ipsec-ah
add action=accept chain=input comment="defconf: accept IPSec ESP" protocol=ipsec-esp
add action=drop chain=input comment="defconf: drop all not coming from LAN" in-interface-list=!LAN

add action=accept chain=forward comment="defconf: accept established,related" connection-state=established,related
add action=drop chain=forward comment="defconf: drop invalid" connection-state=invalid
add action=drop chain=forward comment="defconf: drop bad forward IPs" src-address-list=no_forward_ipv6
add action=drop chain=forward comment="defconf: drop bad forward IPs" dst-address-list=no_forward_ipv6
add action=drop chain=forward comment="defconf: rfc4890 drop hop-limit=1" hop-limit=equal:1 protocol=icmpv6
add action=accept chain=forward comment="defconf: accept ICMPv6 after RAW" protocol=icmpv6
add action=accept chain=forward comment="defconf: accept HIP" protocol=139
add action=accept chain=forward comment="defconf: accept IKE" dst-port=500,4500 protocol=udp
add action=accept chain=forward comment="defconf: accept AH" protocol=ipsec-ah
add action=accept chain=forward comment="defconf: accept ESP" protocol=ipsec-esp
add action=accept chain=forward comment="defconf: accept all that matches IPSec policy" ipsec-policy=in,ipsec
add action=drop chain=forward comment="defconf: drop everything else not coming from LAN" in-interface-list=!LAN

add action=jump chain=forward connection-state=new jump-target=detect_ddos comment="ddosconf: DDoS"
add action=return chain=detect_ddos dst-limit=32,32,src-and-dst-addresses/10s protocol=tcp tcp-flags=syn,ack comment="ddosconf: SYN-ACK Flood" log=yes log-prefix=fw_syn_ack_detected
add action=return chain=detect_ddos dst-limit=96,64,src-and-dst-addresses/10s comment="ddosconf: DDoS"
add action=add-dst-to-address-list chain=detect_ddos address-list=ddos_targets_ipv6 address-list-timeout=10m comment="ddosconf: DDoS"
add action=add-src-to-address-list chain=detect_ddos address-list=ddos_attackers_ipv6 address-list-timeout=10m comment="ddosconf: DDoS" log=yes log-prefix=fw_ddos_attackers


/ipv6 firewall nat

add action=masquerade chain=srcnat comment="defconf: masquerade IPv6" out-interface-list=WAN disabled=yes

add action=accept chain=dstnat dst-port=53 in-interface-list=LAN protocol=udp src-address-list=local_dns_ipv6 comment="lanconf: Accept Local DNS Startup Query (UDP)"
add action=accept chain=dstnat dst-port=53 in-interface-list=LAN protocol=tcp src-address-list=local_dns_ipv6 comment="lanconf: Accept Local DNS Startup Query (TCP)" log=yes log-prefix=fw_dnsv6_tcp

add action=redirect chain=dstnat dst-port=53 in-interface-list=LAN protocol=udp to-ports=53 comment="lanconf: DNS Redirect to Local (UDP)"
add action=redirect chain=dstnat dst-port=53 in-interface-list=LAN protocol=tcp to-ports=53 comment="lanconf: DNS Redirect to Local (TCP)" log=yes log-prefix=fw_dnsv6_tcp


/ipv6 firewall mangle

add action=change-mss chain=forward comment="defconf: Fix IPv6 MSS For WAN" new-mss=clamp-to-pmtu passthrough=yes protocol=tcp tcp-flags=syn


/ipv6 firewall raw

add action=accept chain=prerouting comment="defconf: enable for transparent firewall" disabled=yes

add action=drop chain=prerouting dst-address-list=ddos_targets_ipv6 src-address-list=ddos_attackers_ipv6 comment="ddosconf: DDoS"

add action=accept chain=prerouting comment="defconf: RFC4291, section 2.7.1" dst-address=ff02:0:0:0:0:1:ff00::/104 icmp-options=135 protocol=icmpv6 src-address=::/128

add action=drop chain=prerouting comment="defconf: drop bogon IP's" src-address-list=bad_ipv6
add action=drop chain=prerouting comment="defconf: drop bogon IP's" dst-address-list=bad_ipv6
add action=drop chain=prerouting comment="defconf: drop packets with bad SRC ipv6" src-address-list=bad_src_ipv6
add action=drop chain=prerouting comment="defconf: drop packets with bad DST ipv6" dst-address-list=bad_dst_ipv6

add action=drop chain=prerouting comment="defconf: drop non global from WAN" in-interface-list=WAN src-address-list=not_global_ipv6

add action=drop chain=prerouting comment="defconf: drop bad UDP" port=0 protocol=udp log=yes log-prefix=fw_udp_0_port

add action=jump chain=prerouting comment="defconf: jump to ICMPv6 chain" jump-target=icmp6 protocol=icmpv6
add action=jump chain=prerouting comment="defconf: jump to TCP chain" jump-target=bad_tcp protocol=tcp

add action=accept chain=prerouting comment="defconf: accept local multicast scope" dst-address=ff02::/16
add action=drop chain=prerouting comment="defconf: drop other multicast destinations" dst-address=ff00::/8

add action=accept chain=prerouting comment="defconf: accept everything else from LAN" in-interface-list=LAN
add action=accept chain=prerouting comment="defconf: accept everything else from WAN" in-interface-list=WAN

add action=drop chain=prerouting comment="defconf: drop the rest"

add action=drop chain=bad_tcp comment="defconf: TCP flag filter" protocol=tcp tcp-flags=!fin,!syn,!rst,!ack
add action=drop chain=bad_tcp comment="defconf: tcp-flags=fin,syn" protocol=tcp tcp-flags=fin,syn
add action=drop chain=bad_tcp comment="defconf: tcp-flags=fin,rst" protocol=tcp tcp-flags=fin,rst
add action=drop chain=bad_tcp comment="defconf: tcp-flags=fin,!ack" protocol=tcp tcp-flags=fin,!ack
add action=drop chain=bad_tcp comment="defconf: tcp-flags=fin,urg" protocol=tcp tcp-flags=fin,urg
add action=drop chain=bad_tcp comment="defconf: tcp-flags=syn,rst" protocol=tcp tcp-flags=syn,rst
add action=drop chain=bad_tcp comment="defconf: tcp-flags=rst,urg" protocol=tcp tcp-flags=rst,urg
add action=drop chain=bad_tcp comment="defconf: TCP port 0 drop" port=0 protocol=tcp log=yes log-prefix=fw_tcp_0_port

add action=accept chain=icmp6 comment="defconf: rfc4890 drop ll if hop-limit!=255" dst-address=fe80::/10 hop-limit=not-equal:255 protocol=icmpv6
add action=accept chain=icmp6 comment="defconf: dst unreachable" icmp-options=1:0-255 protocol=icmpv6
add action=accept chain=icmp6 comment="defconf: packet too big" icmp-options=2:0-255 protocol=icmpv6
add action=accept chain=icmp6 comment="defconf: limit exceeded" icmp-options=3:0-1 protocol=icmpv6
add action=accept chain=icmp6 comment="defconf: bad header" icmp-options=4:0-2 protocol=icmpv6

add action=accept chain=icmp6 comment="defconf: echo request limit 10,10" icmp-options=128:0-255 limit=10,10:packet protocol=icmpv6
add action=accept chain=icmp6 comment="defconf: echo reply limit 10,10" icmp-options=129:0-255 limit=10,10:packet protocol=icmpv6

add action=accept chain=icmp6 comment="defconf: rfc4890 multicast listener query limit 10,10 only LAN" src-address=fe80::/10 icmp-options=130:0-255 in-interface-list=LAN limit=10,10:packet protocol=icmpv6
add action=accept chain=icmp6 comment="defconf: rfc4890 multicast listener query report limit 10,10 only LAN" src-address=fe80::/10 icmp-options=131:0-255 in-interface-list=LAN limit=10,10:packet protocol=icmpv6
add action=accept chain=icmp6 comment="defconf: rfc4890 multicast listener query done limit 10,10 only LAN" src-address=fe80::/10 icmp-options=132:0-255 in-interface-list=LAN limit=10,10:packet protocol=icmpv6
add action=accept chain=icmp6 comment="defconf: rfc4890 router solic limit 10,10 only LAN" hop-limit=equal:255 icmp-options=133:0-255 in-interface-list=LAN limit=10,10:packet protocol=icmpv6
add action=accept chain=icmp6 comment="defconf: rfc4890 router advert limit 10,10 only LAN" hop-limit=equal:255 icmp-options=134:0-255 in-interface-list=LAN limit=10,10:packet protocol=icmpv6
add action=accept chain=icmp6 comment="defconf: rfc4890 neighbor solic limit 10,10 only LAN" hop-limit=equal:255 icmp-options=135:0-255 in-interface-list=LAN limit=10,10:packet protocol=icmpv6
add action=accept chain=icmp6 comment="defconf: rfc4890 neighbor advert limit 10,10 only LAN" hop-limit=equal:255 icmp-options=136:0-255 in-interface-list=LAN limit=10,10:packet protocol=icmpv6

add action=accept chain=icmp6 comment="defconf: rfc4890 inverse ND solic limit 10,10 only LAN" hop-limit=equal:255 icmp-options=141:0-255 in-interface-list=LAN limit=10,10:packet protocol=icmpv6
add action=accept chain=icmp6 comment="defconf: rfc4890 inverse ND advert limit 10,10 only LAN" hop-limit=equal:255 icmp-options=142:0-255 in-interface-list=LAN limit=10,10:packet protocol=icmpv6
add action=accept chain=icmp6 comment="defconf: rfc4890 multicast listener report v2 limit 10,10 only LAN" src-address=fe80::/10 icmp-options=143:0-255 in-interface-list=LAN limit=10,10:packet protocol=icmpv6
add action=accept chain=icmp6 comment="defconf: rfc4890 certificate path solicitation limit 10,10 only LAN" hop-limit=equal:255 icmp-options=148:0-255 in-interface-list=LAN limit=10,10:packet protocol=icmpv6
add action=accept chain=icmp6 comment="defconf: rfc4890 certificate path advertisement limit 10,10 only LAN" hop-limit=equal:255 icmp-options=149:0-255 in-interface-list=LAN limit=10,10:packet protocol=icmpv6

add action=accept chain=icmp6 comment="defconf: rfc4890 multicast router advertisement limit 10,10 only LAN" src-address=fe80::/10 hop-limit=equal:1 icmp-options=151:0-255 in-interface-list=LAN limit=10,10:packet protocol=icmpv6
add action=accept chain=icmp6 comment="defconf: rfc4890 multicast router solicitation limit 10,10 only LAN" src-address=fe80::/10 hop-limit=equal:1 icmp-options=152:0-255 in-interface-list=LAN limit=10,10:packet protocol=icmpv6
add action=accept chain=icmp6 comment="defconf: rfc4890 multicast router termination limit 10,10 only LAN" src-address=fe80::/10 hop-limit=equal:1 icmp-options=153:0-255 in-interface-list=LAN limit=10,10:packet protocol=icmpv6

add action=drop chain=icmp6 comment="defconf: drop other icmp" protocol=icmpv6
## 第七部分完毕

## 执行第八部分
## 设置系统日志，格式化过程缓慢需要等待
/disk
format-drive slot1 mbr-partition-table=no file-system=ext4 label=logdrive ## Waiting

/system logging action
add disk-file-count=100 disk-file-name=/slot1/offline_log name=syslog target=disk

/system logging
add action=syslog topics=critical
add action=syslog topics=error
add action=syslog topics=warning
add action=syslog topics=system
add action=syslog topics=script
add action=syslog topics=firewall
add action=syslog topics=interface
## 第八部分完毕

## 执行第九部分
# 1.设置系统邮件和自动升级脚本内容。
# 2.检查系统账户权限。
# 3.检查不必要的 IPv4 DHCP Client。
# 4.启用 PPPoE 拨号。
# 5.申请系统 License。
# 6.备份系统。
# 7.重启系统。
## 第九部分完毕

