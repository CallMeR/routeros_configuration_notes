## 说明
# 01. 将 PPPoE 拨号的账户根据实际情况修改，并设置密码。
# 02. 将内网网段 172.16.1.0/24 和 RouterOS IPv4 地址 172.16.1.1 根据实际情况修改。
# 03. 将光猫网段 192.168.100.0/24 和 ether1 IPv4 地址 192.168.100.2 根据实际情况修改。
# 04. 系统 DNS 服务器已设置为 DNSPod 和 AliDNS 。
# 05. 本脚本将使用 172.16.1.1 作为内网设备 DNSv4 服务器。
# 06. 新系统管理员账号 用户名、密码 需要修改，账户添加完成后，用新管理员账户执行后续命令条目。
# 07. 防火墙已默认启用 fasttrack-connection 。
# 08. 系统日志邮件的 发件箱、收件箱、SMTP密码 需要根据实际情况修改。
# 09. DHCPv4 中 MAC 地址绑定静态 IP ，具体参数需要根据实际情况修改。
# 10. 将内网 IPv6 地址 fdac::/64 换成合法的 ULA 地址。
# 11. 本脚本将使用 fdac::1 作为内网设备 DNSv6 服务器。
# 12. RB750Gr3 使用 TF 卡作为附加硬盘，硬盘格式化过程中会忽略后续命令，需要等待格式化完成后再执行后续命令条目。

## 执行第一部分
## 配置网口
/interface ethernet
set [ find default-name=ether1 ] comment="defconf: Local WAN"
set [ find default-name=ether2 ] comment="defconf: Local LAN1"
set [ find default-name=ether3 ] comment="defconf: Local LAN2"
set [ find default-name=ether4 ] comment="defconf: Local LAN3"
set [ find default-name=ether5 ] comment="defconf: Local LAN4"

/interface bridge
add auto-mac=yes comment="defconf: Local Bridge" name=bridge1

/interface bridge port
add bridge=bridge1 interface=ether2
add bridge=bridge1 interface=ether3
add bridge=bridge1 interface=ether4
add bridge=bridge1 interface=ether5
## 第一部分完毕

## 执行第二部分
## PPPoE & DNS & DHCP
/interface pppoe-client
add add-default-route=yes use-peer-dns=no comment="defconf: Local PPPoE IPv4" disabled=yes interface=ether1 name=pppoe-out1 user="<your_pppoe_user_name>" password="<your_pppoe_user_password>"

/ip address
add address=172.16.1.1/24 comment="defconf: Local LAN IPv4 Address" interface=bridge1 network=172.16.1.0
add address=192.168.100.2/24 comment="onuconf: Link IPv4 Address For ONU" interface=ether1 network=192.168.100.0

/ip pool
add comment="defconf: Local LAN IPv4 Pool" name=Local_Pool_IPv4 ranges=172.16.1.101-172.16.1.150

/ip dhcp-server
add address-pool=Local_Pool_IPv4 bootp-support=none comment="defconf: Local LAN IPv4 DHCP Server" interface=bridge1 lease-time=1d name=Local_DHCP_IPv4

/ip dhcp-server network
add address=172.16.1.0/24 comment="defconf: Local LAN IPv4 DHCP Network" dns-server=172.16.1.1 domain=fox.local gateway=172.16.1.1 netmask=24

/ip dns
set allow-remote-requests=yes cache-max-ttl=6h cache-size=1024KiB max-concurrent-queries=150 servers=119.29.29.29,223.5.5.5,223.6.6.6,2402:4e00::,2400:3200::1,2400:3200:baba::1

/ip dhcp-client
remove numbers=[find where interface ~ "ether1"]
## 第二部分完毕

## 执行第三部分
## 系统参数调整
/system identity
set name=FoxRouter

/ipv6 settings
set disable-ipv6=yes

/system clock
set time-zone-name=Asia/Shanghai

/system ntp client
set enabled=yes

/system ntp client servers
add address=ntp.aliyun.com
add address=ntp.tencent.com

/ip service
set telnet address=172.16.1.0/24 disabled=yes
set ftp address=172.16.1.0/24 disabled=yes
set www address=172.16.1.0/24
set ssh address=172.16.1.0/24
set www-ssl address=172.16.1.0/24
set api address=172.16.1.0/24 disabled=yes
set winbox address=172.16.1.0/24
set api-ssl address=172.16.1.0/24 disabled=yes

/user group
set read policy=read,winbox,web,!local,!telnet,!ssh,!ftp,!reboot,!write,!policy,!test,!password,!sniff,!sensitive,!api,!romon,!rest-api

/user
add name="<your_ros_user_name>" password="<your_ros_user_password>" group=full address=172.16.1.0/24 comment="defconf: System Admin User"
set admin group=read address=172.16.1.0/24 comment="defconf: System Default User"
## 第三部分完毕

## 执行第四部分
## 防火墙 & QoS
## Filter规则9条+虚拟规则1条
## NAT规则4条
## Mangle规则2条+虚拟规则3条
## Address-list规则2条
/interface list
add name=WAN comment="defconf: Connect To Global"
add name=LAN comment="defconf: Local Bridge"
add name=ONU comment="onuconf: Access To ONU"

/interface list member
add interface=pppoe-out1 list=WAN comment="defconf: Connect To Global"
add interface=bridge1 list=LAN comment="defconf: Local Bridge"
add interface=ether1 list=ONU comment="onuconf: Access To ONU"


/ip firewall address-list

add address=192.168.100.1 comment="onuconf: local ONU address" list=local_onu_ipv4
add address=172.16.1.0/24 comment="lanconf: local LAN address" list=local_lan_ipv4


/ip firewall filter

add chain=input action=accept connection-state=established,related comment="defconf: accept established,related"
add chain=input action=drop connection-state=invalid comment="defconf: drop invalid"
add chain=input action=accept protocol=icmp comment="defconf: accept ICMP"
add chain=input action=drop in-interface-list=!LAN comment="defconf: drop all not coming from LAN"

add chain=forward action=fasttrack-connection connection-state=established,related comment="defconf: fasttrack"
add chain=forward action=accept connection-state=established,related comment="defconf: accept established,related"
add chain=forward action=drop connection-state=invalid comment="defconf: drop invalid"
add chain=forward action=drop connection-state=new connection-nat-state=!dstnat in-interface-list=WAN comment="defconf: drop all from WAN not DSTNATed" log=yes log-prefix=fw_wan_not_DSTNATed
add chain=forward action=drop connection-state=new connection-nat-state=!dstnat in-interface-list=ONU comment="onuconf: drop all from ONU not DSTNATed" log=yes log-prefix=fw_onu_not_DSTNATed


/ip firewall nat

add action=masquerade chain=srcnat comment="defconf: masquerade IPv4" out-interface-list=WAN
add action=masquerade chain=srcnat comment="onuconf: access to ONU" out-interface-list=ONU src-address-list=local_lan_ipv4 dst-address-list=local_onu_ipv4

add action=redirect chain=dstnat comment="lanconf: redirect DNS query (UDP)" dst-port=53 in-interface-list=LAN protocol=udp to-ports=53
add action=redirect chain=dstnat comment="lanconf: redirect DNS query (TCP)" dst-port=53 in-interface-list=LAN protocol=tcp to-ports=53 log=yes log-prefix=fw_dnsv4_tcp


/ip firewall mangle

add action=change-mss chain=forward comment="defconf: fix IPv4 mss For WAN" new-mss=clamp-to-pmtu passthrough=yes protocol=tcp tcp-flags=syn
add action=accept chain=prerouting comment="onuconf: access to ONU" src-address-list=local_lan_ipv4 dst-address-list=local_onu_ipv4


/ip settings
set max-neighbor-entries=1024 rp-filter=loose tcp-syncookies=yes

/ip neighbor discovery-settings
set discover-interface-list=none

/ip proxy
set enabled=no

/ip socks
set enabled=no

/ip upnp
set enabled=no

/ip cloud
set ddns-enabled=no update-time=no

/ip ssh
set strong-crypto=yes

/tool mac-server
set allowed-interface-list=none

/tool mac-server mac-winbox
set allowed-interface-list=none

/tool mac-server ping
set enabled=no

/tool bandwidth-server
set enabled=no
## 第四部分完毕

## 执行第五部分
## 定时任务
/tool/e-mail/set address="<smtp.domain_a.com>" from="<your_email_a@domain_a.com>" port=465 tls=yes user="<your_email_a@domain_a.com>" password="<your_smtp_password>"

/system scheduler

add comment="sysconf: system alert log email" interval=45m name=system_log_timer on-event="/system script run email_log_worker" policy=read,write,policy,test start-time=00:00:00
add comment="sysconf: system resource email" interval=6h name=system_res_timer on-event="/system script run email_res_worker" policy=read,write,policy,test start-time=00:05:00

add comment="sysconf: system os auto upgrade" interval=1d name=system_upgrade_timer on-event="/system script run sys_upgrade_worker" policy=reboot,read,write,policy,password start-time=02:55:00

add comment="pppoeconf: system disable PPPoE" interval=3d name=disable_pppoe_timer on-event="/interface disable pppoe-out1" policy=write start-time=04:00:00
add comment="pppoeconf: system enable PPPoE" interval=3d name=enable_pppoe_timer on-event="/interface enable pppoe-out1" policy=write start-time=04:00:10

/system script

add comment="sysconf: system alert log email" dont-require-permissions=no name=email_log_worker policy=read,write,policy,test source=""
add comment="sysconf: system resource email" dont-require-permissions=no name=email_res_worker policy=read,write,policy,test source=""

add comment="sysconf: system os auto upgrade" dont-require-permissions=no name=sys_upgrade_worker policy=reboot,read,write,policy,password source=""
## 第五部分完毕

## 执行第六部分
## 静态IP
/ip dhcp-server lease

add address=172.16.1.10 comment="<Your_Device_Name1>" lease-time=2d mac-address=AA:BB:CC:00:00:10 server=Local_DHCP_IPv4
## 第六部分完毕

## 执行第七部分
## 设置IPv6
## Filter规则21条
## NAT规则3条
## Mangle规则1条
## Address-list规则9条
/ipv6 settings
set disable-ipv6=no max-neighbor-entries=1024

/ipv6 dhcp-client
add add-default-route=no comment="defconf: DHCPv6 on PPPoE" interface=pppoe-out1 pool-name=Local_Pool_GUA_IPv6 request=prefix use-peer-dns=no

/ipv6 address
add address=::1 advertise=yes comment="defconf: Local LAN GUA IPv6 Address" from-pool=Local_Pool_GUA_IPv6 interface=bridge1
add address=fdac::1 advertise=yes comment="defconf: Local LAN ULA IPv6 Address" interface=bridge1

/ipv6 nd prefix default
set preferred-lifetime=1h valid-lifetime=2h

/ipv6 nd
set [ find default=yes ] disabled=yes
add interface=bridge1 ra-interval=1m-2m hop-limit=64 dns=fdac::1 advertise-mac-address=yes advertise-dns=yes


/ipv6 firewall address-list

add list=bad_ipv6 address=::/128 comment="defconf: unspecified address"
add list=bad_ipv6 address=::1 comment="defconf: lo"
add list=bad_ipv6 address=fec0::/10 comment="defconf: site-local"
add list=bad_ipv6 address=::ffff:0:0/96 comment="defconf: ipv4-mapped"
add list=bad_ipv6 address=::/96 comment="defconf: ipv4 compat"
add list=bad_ipv6 address=100::/64 comment="defconf: discard only "
add list=bad_ipv6 address=2001:db8::/32 comment="defconf: documentation"
add list=bad_ipv6 address=2001:10::/28 comment="defconf: ORCHID"
add list=bad_ipv6 address=3ffe::/16 comment="defconf: 6bone"


/ipv6 firewall filter

add chain=input action=accept connection-state=established,related comment="defconf: accept established,related"
add chain=input action=drop connection-state=invalid comment="defconf: drop invalid"
add chain=input action=accept protocol=icmpv6 comment="defconf: accept ICMPv6"
add chain=input action=accept protocol=udp port=33434-33534 comment="defconf: accept UDP traceroute"
add chain=input action=accept protocol=udp dst-port=546 src-address=fe80::/16 comment="defconf: accept DHCPv6-Client prefix delegation" log=yes log-prefix=fw_ipv6_pd
add chain=input action=accept protocol=udp dst-port=500,4500 comment="defconf: accept IKE"
add chain=input action=accept protocol=ipsec-ah comment="defconf: accept ipsec AH"
add chain=input action=accept protocol=ipsec-esp comment="defconf: accept ipsec ESP"
add chain=input action=drop in-interface-list=!LAN comment="defconf: drop everything else not coming from LAN"

add chain=forward action=accept connection-state=established,related comment="defconf: accept established,related"
add chain=forward action=drop connection-state=invalid comment="defconf: drop invalid"
add chain=forward action=drop src-address-list=bad_ipv6 comment="defconf: drop packets with bad src ipv6"
add chain=forward action=drop dst-address-list=bad_ipv6 comment="defconf: drop packets with bad dst ipv6"
add chain=forward action=drop protocol=icmpv6 hop-limit=equal:1 comment="defconf: rfc4890 drop hop-limit=1"
add chain=forward action=accept protocol=icmpv6 comment="defconf: accept ICMPv6"
add chain=forward action=accept protocol=139 comment="defconf: accept HIP"
add chain=forward action=accept protocol=udp dst-port=500,4500 comment="defconf: accept IKE"
add chain=forward action=accept protocol=ipsec-ah comment="defconf: accept ipsec AH"
add chain=forward action=accept protocol=ipsec-esp comment="defconf: accept ipsec ESP"
add chain=forward action=accept ipsec-policy=in,ipsec comment="defconf: accept all that matches ipsec policy"
add chain=forward action=drop in-interface-list=!LAN comment="defconf: drop everything else not coming from LAN"


/ipv6 firewall nat

add action=masquerade chain=srcnat comment="defconf: masquerade IPv6" out-interface-list=WAN disabled=yes

add action=redirect chain=dstnat comment="lanconf: redirect DNS query (UDP)" dst-port=53 in-interface-list=LAN protocol=udp to-ports=53
add action=redirect chain=dstnat comment="lanconf: redirect DNS query (TCP)" dst-port=53 in-interface-list=LAN protocol=tcp to-ports=53 log=yes log-prefix=fw_dnsv6_tcp


/ipv6 firewall mangle

add action=change-mss chain=forward comment="defconf: fix IPv6 mss For WAN" new-mss=clamp-to-pmtu passthrough=yes protocol=tcp tcp-flags=syn
## 第七部分完毕

## 执行第八部分
## 设置系统日志，格式化过程缓慢需要等待
/disk
format-drive sd1 mbr-partition-table=no file-system=fat32 label=logdrive ## Waiting

/system logging action
add disk-file-count=100 disk-file-name=/sd1/offline_log name=syslog target=disk

/system logging
add action=syslog topics=critical
add action=syslog topics=error
add action=syslog topics=warning
add action=syslog topics=system
add action=syslog topics=script
add action=syslog topics=firewall
add action=syslog topics=interface
## 第八部分完毕

## 执行第九部分
# 1.设置系统邮件和自动升级脚本内容。
# 2.检查系统账户权限。
# 3.检查不必要的 IPv4 DHCP Client。
# 4.启用 PPPoE 拨号。
# 5.备份系统。
# 6.重启系统。
## 第九部分完毕

