## 说明
# 01. 将内网网段 172.16.1.0/24 和 RouterOS IPv4 地址 172.16.1.1 根据实际情况修改。
# 02. 系统 DNS 服务器已设置为 DNSPod 和 AliDNS 。
# 03. 本脚本将使用 172.16.1.1 作为内网设备 DNSv4 服务器。
# 04. 新系统管理员账号 用户名、密码 需要修改，账户添加完成后，用新管理员账户执行后续命令条目。
# 05. 防火墙已默认启用 fasttrack-connection 。
# 06. QoS 使用 Fq-CoDel 算法以及 Queue Tree ， Qos 的带宽限速需要根据签约带宽进行修改。
# 07. 系统日志邮件的 发件箱、收件箱、SMTP密码 需要根据实际情况修改。
# 08. DHCPv4 中 MAC 地址绑定静态 IP ，具体参数需要根据实际情况修改。
# 09. RB750Gr3 使用 TF 卡作为附加硬盘，硬盘格式化过程中会忽略后续命令，需要等待格式化完成后再执行后续命令条目。


## 第一部分 - 配置网口
/interface ethernet
set [ find default-name=ether1 ] comment="defconf: local WAN"
set [ find default-name=ether2 ] comment="defconf: local LAN"
set [ find default-name=ether3 ] comment="defconf: local LAN"
set [ find default-name=ether4 ] comment="defconf: local LAN"
set [ find default-name=ether5 ] comment="defconf: local LAN"

/interface bridge
add name=bridge1 comment="defconf: local Bridge" auto-mac=yes

/interface bridge port
add bridge=bridge1 interface=ether2
add bridge=bridge1 interface=ether3
add bridge=bridge1 interface=ether4
add bridge=bridge1 interface=ether5

/ip address
add interface=bridge1 comment="defconf: local LAN IPv4 address" address=172.16.1.1/24 network=172.16.1.0

/ip dhcp-client
remove numbers=[find where interface ~ "ether1"]
add interface=ether1 comment="defconf: DHCPv4 for WAN" use-peer-dns=no use-peer-ntp=no

/interface list
add name=WAN comment="defconf: extranet list"
add name=LAN comment="defconf: intranet list"

/interface list member
add list=WAN comment="defconf: extranet member" interface=ether1
add list=LAN comment="defconf: intranet member" interface=bridge1
## 第一部分完成


## 第二部分 - DNS & DHCP & 静态 IPv4 地址绑定
/ip dns
set allow-remote-requests=yes cache-max-ttl=6h cache-size=1024KiB max-concurrent-queries=150 servers=119.29.29.29,223.5.5.5,223.6.6.6,2402:4e00::,2400:3200::1,2400:3200:baba::1

/ip pool
add name=dhcpv4-pool1 comment="defconf: local LAN DHCPv4 pool" ranges=172.16.1.101-172.16.1.200

/ip dhcp-server
add name=dhcpv4-server1 comment="defconf: local LAN DHCPv4 server" address-pool=dhcpv4-pool1 interface=bridge1 lease-time=1d bootp-support=none

/ip dhcp-server network
add address=172.16.1.0/24 comment="defconf: local LAN DHCPv4 network" gateway=172.16.1.1 netmask=24 domain=fox.home.arpa dns-server=172.16.1.1

/ip dhcp-server lease
add address=172.16.1.10 comment="<your-device-name1>" lease-time=2d mac-address=AA:BB:CC:00:00:10 server=dhcpv4-server1
## 第二部分完成


## 第三部分 - IPv4 高级防火墙
##       Filter 规则 15 条 + 虚拟规则 1 条
##          NAT 规则  3 条
##       Mangle 规则  1 条 + 虚拟规则 3 条
##          Raw 规则 40 条 + 虚拟规则 1 条
## Address-list 规则 23 条
##    Blackhole 规则 16 条
/ip firewall address-list

add address=172.16.1.0/24 comment="lanconf: local LAN address" list=local_lan_ipv4

add address=0.0.0.0/8 comment="defconf: RFC6890" list=no_forward_ipv4
add address=169.254.0.0/16 comment="defconf: RFC6890" list=no_forward_ipv4
add address=224.0.0.0/4 comment="defconf: multicast" list=no_forward_ipv4
add address=255.255.255.255/32 comment="defconf: RFC6890" list=no_forward_ipv4

add address=127.0.0.0/8 comment="defconf: RFC6890" list=bad_ipv4
add address=192.0.0.0/24 comment="defconf: RFC6890" list=bad_ipv4
add address=192.0.2.0/24 comment="defconf: RFC6890 documentation" list=bad_ipv4
add address=198.51.100.0/24 comment="defconf: RFC6890 documentation" list=bad_ipv4
add address=203.0.113.0/24 comment="defconf: RFC6890 documentation" list=bad_ipv4
add address=240.0.0.0/4 comment="defconf: RFC6890 reserved" list=bad_ipv4

add address=0.0.0.0/8 comment="defconf: RFC6890" list=not_global_ipv4
add address=100.64.0.0/10 comment="defconf: RFC6890" list=not_global_ipv4
add address=169.254.0.0/16 comment="defconf: RFC6890" list=not_global_ipv4
add address=192.0.0.0/29 comment="defconf: RFC6890" list=not_global_ipv4
add address=198.18.0.0/15 comment="defconf: RFC6890 benchmark" list=not_global_ipv4
add address=255.255.255.255/32 comment="defconf: RFC6890" list=not_global_ipv4

add address=224.0.0.0/4 comment="defconf: multicast" list=bad_src_ipv4
add address=255.255.255.255/32 comment="defconf: RFC6890" list=bad_src_ipv4

add address=0.0.0.0/8 comment="defconf: RFC6890" list=bad_dst_ipv4
add address=224.0.0.0/4 comment="defconf: RFC6890" list=bad_dst_ipv4

add comment="ddosconf: DDoS" list=ddos_targets_ipv4
add comment="ddosconf: DDoS" list=ddos_attackers_ipv4


/ip firewall filter

add action=accept chain=input comment="defconf: accept ICMP after RAW" protocol=icmp
add action=accept chain=input comment="defconf: accept established,related,untracked" connection-state=established,related,untracked
add action=drop chain=input comment="defconf: drop invalid" connection-state=invalid
add action=drop chain=input comment="defconf: drop all not coming from LAN" in-interface-list=!LAN

add action=fasttrack-connection chain=forward comment="defconf: fasttrack" connection-state=established,related disabled=no
add action=accept chain=forward comment="defconf: accept established,related,untracked" connection-state=established,related,untracked
add action=drop chain=forward comment="defconf: drop invalid" connection-state=invalid
add action=drop chain=forward comment="defconf: drop all from WAN not DSTNATed" connection-nat-state=!dstnat connection-state=new in-interface-list=WAN log=yes log-prefix="[wan-not-dnat]"
add action=drop chain=forward comment="defconf: drop bad forward IPs" src-address-list=no_forward_ipv4
add action=drop chain=forward comment="defconf: drop bad forward IPs" dst-address-list=no_forward_ipv4

add action=jump chain=forward comment="ddosconf: DDoS" connection-state=new jump-target=detect-ddos
add action=return chain=detect-ddos comment="ddosconf: DDoS SYN-ACK Flood" dst-limit=50,50,src-and-dst-addresses/10s protocol=tcp tcp-flags=syn,ack log=yes log-prefix="[syn-ack-flood]"
add action=return chain=detect-ddos comment="ddosconf: DDoS" dst-limit=200,200,src-and-dst-addresses/10s
add action=add-dst-to-address-list chain=detect-ddos comment="ddosconf: DDoS" address-list=ddos_targets_ipv4 address-list-timeout=10m
add action=add-src-to-address-list chain=detect-ddos comment="ddosconf: DDoS" address-list=ddos_attackers_ipv4 address-list-timeout=10m log=yes log-prefix="[ddos-ipv4]"


/ip firewall nat

add action=masquerade chain=srcnat comment="defconf: masquerade IPv4" out-interface-list=WAN

add action=redirect chain=dstnat comment="lanconf: redirect DNS query (UDP)" dst-port=53 in-interface-list=LAN protocol=udp to-ports=53
add action=redirect chain=dstnat comment="lanconf: redirect DNS query (TCP)" dst-port=53 in-interface-list=LAN protocol=tcp to-ports=53


/ip firewall mangle

add action=change-mss chain=forward comment="defconf: fix IPv4 mss for WAN" new-mss=clamp-to-pmtu passthrough=yes protocol=tcp tcp-flags=syn


/ip firewall raw

add action=accept chain=prerouting comment="defconf: enable for transparent firewall" disabled=yes

add action=drop chain=prerouting comment="ddosconf: DDoS" dst-address-list=ddos_targets_ipv4 src-address-list=ddos_attackers_ipv4

add action=accept chain=prerouting comment="defconf: accept DHCPv4 discover" dst-address=255.255.255.255 dst-port=67 in-interface-list=LAN protocol=udp src-address=0.0.0.0 src-port=68

add action=drop chain=prerouting comment="defconf: drop bogon IPs" src-address-list=bad_ipv4
add action=drop chain=prerouting comment="defconf: drop bogon IPs" dst-address-list=bad_ipv4
add action=drop chain=prerouting comment="defconf: drop bogon IPs" src-address-list=bad_src_ipv4
add action=drop chain=prerouting comment="defconf: drop bogon IPs" dst-address-list=bad_dst_ipv4

add action=drop chain=prerouting comment="defconf: drop non global from WAN" src-address-list=not_global_ipv4 in-interface-list=WAN
add action=drop chain=prerouting comment="defconf: drop forward to local LAN from WAN" in-interface-list=WAN dst-address-list=local_lan_ipv4 log=yes log-prefix="[wan-to-lan]"

add action=drop chain=prerouting comment="defconf: drop local if not from default IPv4 range" in-interface-list=LAN src-address-list=!local_lan_ipv4

add action=drop chain=prerouting comment="defconf: drop UDP port 0" port=0 protocol=udp log=yes log-prefix="[udp-port-0]"

add action=jump chain=prerouting comment="defconf: jump to ICMP chain" jump-target=icmp4 protocol=icmp
add action=jump chain=prerouting comment="defconf: jump to TCP chain" jump-target=bad-tcp protocol=tcp

add action=accept chain=prerouting comment="defconf: accept everything else from LAN" in-interface-list=LAN
add action=accept chain=prerouting comment="defconf: accept everything else from WAN" in-interface-list=WAN

add action=drop chain=prerouting comment="defconf: drop the rest"

add action=drop chain=bad-tcp comment="defconf: TCP flag filter" protocol=tcp tcp-flags=!fin,!syn,!rst,!ack
add action=drop chain=bad-tcp comment="defconf: drop flags fin,syn" protocol=tcp tcp-flags=fin,syn
add action=drop chain=bad-tcp comment="defconf: drop flags fin,rst" protocol=tcp tcp-flags=fin,rst
add action=drop chain=bad-tcp comment="defconf: drop flags fin,!ack" protocol=tcp tcp-flags=fin,!ack
add action=drop chain=bad-tcp comment="defconf: drop flags fin,urg" protocol=tcp tcp-flags=fin,urg
add action=drop chain=bad-tcp comment="defconf: drop flags syn,rst" protocol=tcp tcp-flags=syn,rst
add action=drop chain=bad-tcp comment="defconf: drop flags rst,urg" protocol=tcp tcp-flags=rst,urg
add action=drop chain=bad-tcp comment="defconf: drop TCP port 0" port=0 protocol=tcp log=yes log-prefix="[tcp-port-0]"

add action=accept chain=icmp4 comment="lanconf: accept echo reply from WAN" icmp-options=0:0 protocol=icmp in-interface-list=WAN
add action=accept chain=icmp4 comment="lanconf: accept net unreachable from WAN" icmp-options=3:0 protocol=icmp in-interface-list=WAN
add action=accept chain=icmp4 comment="lanconf: accept fragmentation needed from WAN" icmp-options=3:4 protocol=icmp in-interface-list=WAN
add action=accept chain=icmp4 comment="lanconf: accept time exceeded from WAN" icmp-options=11:0-255 protocol=icmp in-interface-list=WAN
add action=drop chain=icmp4 comment="lanconf: drop other ICMP from WAN" protocol=icmp in-interface-list=WAN

add action=accept chain=icmp4 comment="defconf: echo reply" icmp-options=0:0 protocol=icmp in-interface-list=LAN log=yes log-prefix="[lan-ehco-reply]"
add action=accept chain=icmp4 comment="defconf: net unreachable" icmp-options=3:0 protocol=icmp in-interface-list=LAN
add action=accept chain=icmp4 comment="defconf: host unreachable" icmp-options=3:1 protocol=icmp in-interface-list=LAN
add action=accept chain=icmp4 comment="defconf: protocol unreachable" icmp-options=3:2 protocol=icmp in-interface-list=LAN
add action=accept chain=icmp4 comment="defconf: port unreachable" icmp-options=3:3 protocol=icmp in-interface-list=LAN
add action=accept chain=icmp4 comment="defconf: fragmentation needed" icmp-options=3:4 protocol=icmp in-interface-list=LAN

add action=accept chain=icmp4 comment="defconf: echo to local device" icmp-options=8:0 protocol=icmp in-interface-list=LAN dst-address-list=local_lan_ipv4
add action=drop chain=icmp4 comment="defconf: echo to non global" icmp-options=8:0 protocol=icmp in-interface-list=LAN dst-address-list=not_global_ipv4
add action=accept chain=icmp4 comment="defconf: echo to WAN" icmp-options=8:0 protocol=icmp in-interface-list=LAN
add action=accept chain=icmp4 comment="defconf: time exceeded" icmp-options=11:0-255 protocol=icmp in-interface-list=LAN
add action=drop chain=icmp4 comment="defconf: drop all other ICMP" protocol=icmp


/ip settings
set max-neighbor-entries=1024 rp-filter=loose tcp-syncookies=yes

/ip neighbor discovery-settings
set discover-interface-list=none

/ip proxy
set enabled=no

/ip socks
set enabled=no

/ip upnp
set enabled=no

/ip cloud
set ddns-enabled=no update-time=no

/ip ssh
set strong-crypto=yes

/tool mac-server
set allowed-interface-list=none

/tool mac-server mac-winbox
set allowed-interface-list=none

/tool mac-server ping
set enabled=no

/tool bandwidth-server
set enabled=no


/ip route
add blackhole comment="defconf: RFC6890 - this host on this network" disabled=no dst-address=0.0.0.0/8
add blackhole comment="defconf: RFC6890 - private use" disabled=no dst-address=10.0.0.0/8
add blackhole comment="defconf: RFC6890 - shared address space" disabled=no dst-address=100.64.0.0/10
add blackhole comment="defconf: RFC6890 - loopback" disabled=no dst-address=127.0.0.0/8
add blackhole comment="defconf: RFC6890 - link local" disabled=no dst-address=169.254.0.0/16
add blackhole comment="defconf: RFC6890 - private use" disabled=no dst-address=172.16.0.0/12
add blackhole comment="defconf: RFC6890 - IETF protocol assignments" disabled=no dst-address=192.0.0.0/24
add blackhole comment="defconf: RFC6890 - DS-Lite" disabled=no dst-address=192.0.0.0/29
add blackhole comment="defconf: RFC6890 - TEST-NET-1" disabled=no dst-address=192.0.2.0/24
add blackhole comment="defconf: RFC6890 - 6to4 relay anycast" disabled=no dst-address=192.88.99.0/24
add blackhole comment="defconf: RFC6890 - private use" disabled=no dst-address=192.168.0.0/16
add blackhole comment="defconf: RFC6890 - benchmarking" disabled=no dst-address=198.18.0.0/15
add blackhole comment="defconf: RFC6890 - TEST-NET-2" disabled=no dst-address=198.51.100.0/24
add blackhole comment="defconf: RFC6890 - TEST-NET-3" disabled=no dst-address=203.0.113.0/24
add blackhole comment="defconf: RFC6890 - reserved" disabled=no dst-address=240.0.0.0/4
add blackhole comment="defconf: RFC6890 - limited broadcast" disabled=no dst-address=255.255.255.255/32
## 第三部分完成


## 第四部分 - QoS 流控
/queue type
add name=fq-codel-rx kind=fq-codel fq-codel-limit=1000 fq-codel-quantum=500 fq-codel-memlimit=16.0MiB fq-codel-interval=60ms fq-codel-ecn=yes
add name=fq-codel-tx kind=fq-codel fq-codel-limit=800 fq-codel-quantum=300 fq-codel-memlimit=16.0MiB fq-codel-interval=60ms fq-codel-ecn=no

/queue tree
add name=fq-codel-download comment="qosconf: download queue with Fq-CoDel" bucket-size=0.05 max-limit=500M packet-mark=no-mark parent=bridge1 queue=fq-codel-rx
add name=fq-codel-upload comment="qosconf: upload queue with Fq-CoDel" bucket-size=0.03 max-limit=50M packet-mark=no-mark parent=ether1 queue=fq-codel-tx
## 第四部分完成


## 第五部分 - 系统参数调整
/system identity
set name=FoxRouter

/ipv6 settings
set disable-ipv6=yes

/system clock
set time-zone-name=Asia/Shanghai

/system ntp client
set enabled=yes

/system ntp client servers
add address=ntp.aliyun.com
add address=ntp.tencent.com

/ip service
set telnet address=172.16.1.0/24 disabled=yes
set ftp address=172.16.1.0/24 disabled=yes
set www address=172.16.1.0/24
set ssh address=172.16.1.0/24
set www-ssl address=172.16.1.0/24
set api address=172.16.1.0/24 disabled=yes
set winbox address=172.16.1.0/24
set api-ssl address=172.16.1.0/24 disabled=yes

/user group
set read policy=read,winbox,web,!local,!telnet,!ssh,!ftp,!reboot,!write,!policy,!test,!password,!sniff,!sensitive,!api,!romon,!rest-api

/user
add name="<your-ros-user-name>" password="<your-ros-user-password>" group=full address=172.16.1.0/24 comment="defconf: system admin user"
set admin group=read address=172.16.1.0/24 comment="defconf: system default user"
## 第五部分完成


## 第六部分 - 定时任务
/tool e-mail
set address="<smtp.xxx.com>" from="<your-email-x@xxx.com>" port=465 tls=yes user="<your-email-x@xxx.com>" password="<your-smtp-password>"

/system scheduler
add comment="sysconf: system alert log email" interval=45m name=system-log-timer on-event="/system script run email-log-worker" policy=read,write,policy,test start-time=00:00:00
add comment="sysconf: system resource email" interval=6h name=system-res-timer on-event="/system script run email-res-worker" policy=read,write,policy,test start-time=00:05:00
add comment="sysconf: system os auto upgrade" interval=1d name=system-upgrade-timer on-event="/system script run sys-upgrade-worker" policy=reboot,read,write,policy,password start-time=02:55:00
add comment="dhcpconf: system disable DHCPv4 client" interval=1d name=disable-dhcpv4-timer on-event="/ip dhcp-client disable numbers=[find where interface ~ \"ether1\"]" policy=write start-time=04:00:00
add comment="dhcpconf: system enable DHCPv4 client" interval=1d name=enable-dhcpv4-timer on-event="/ip dhcp-client enable numbers=[find where interface ~ \"ether1\"]" policy=write start-time=04:00:10

/system script
add comment="sysconf: system alert log email" dont-require-permissions=no name=email-log-worker policy=read,write,policy,test source=""
add comment="sysconf: system resource email" dont-require-permissions=no name=email-res-worker policy=read,write,policy,test source=""
add comment="sysconf: system os auto upgrade" dont-require-permissions=no name=sys-upgrade-worker policy=reboot,read,write,policy,password source=""
## 第六部分完成


## 第七部分 - 设置系统日志
## 格式化过程缓慢需要等待
/disk
format-drive sd1 mbr-partition-table=no file-system=fat32 label=logdrive ## Waiting

/system logging action
add disk-file-count=100 disk-file-name=/sd1/offline_log name=syslog target=disk

/system logging
add action=syslog topics=critical
add action=syslog topics=error
add action=syslog topics=warning
add action=syslog topics=system
add action=syslog topics=script
add action=syslog topics=firewall
add action=syslog topics=interface
## 第七部分完成


## 第八部分
# 1.设置系统邮件和自动升级脚本内容。
# 2.检查系统账户权限。
# 3.备份系统。
# 4.重启系统。
## 第八部分完成

