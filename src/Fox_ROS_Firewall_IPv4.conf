/ip firewall address-list

add address=192.168.100.1 comment="modemconf: Modem Address" list=modem_ipv4
add address=172.16.1.0/24 comment="lanconf: Local Address" list=local_subnet_ipv4

add address=172.16.1.1 comment="lanconf: Local DNS Address" list=local_dns_ipv4
add address=172.16.1.2 comment="lanconf: Local DNS Address" list=local_dns_ipv4
add address=172.16.1.3 comment="lanconf: Local DNS Address" list=local_dns_ipv4

add address=0.0.0.0/8 comment="defconf: RFC6890" list=no_forward_ipv4
add address=169.254.0.0/16 comment="defconf: RFC6890" list=no_forward_ipv4
add address=224.0.0.0/4 comment="defconf: multicast" list=no_forward_ipv4
add address=255.255.255.255/32 comment="defconf: RFC6890" list=no_forward_ipv4

add address=127.0.0.0/8 comment="defconf: RFC6890" list=bad_ipv4
add address=192.0.0.0/24 comment="defconf: RFC6890" list=bad_ipv4
add address=192.0.2.0/24 comment="defconf: RFC6890 documentation" list=bad_ipv4
add address=198.51.100.0/24 comment="defconf: RFC6890 documentation" list=bad_ipv4
add address=203.0.113.0/24 comment="defconf: RFC6890 documentation" list=bad_ipv4
add address=240.0.0.0/4 comment="defconf: RFC6890 reserved" list=bad_ipv4

add address=0.0.0.0/8 comment="defconf: RFC6890" list=not_global_ipv4
add address=10.0.0.0/8 comment="defconf: RFC6890" list=not_global_ipv4
add address=100.64.0.0/10 comment="defconf: RFC6890" list=not_global_ipv4
add address=169.254.0.0/16 comment="defconf: RFC6890" list=not_global_ipv4
add address=172.16.0.0/12 comment="defconf: RFC6890" list=not_global_ipv4
add address=192.0.0.0/29 comment="defconf: RFC6890" list=not_global_ipv4
add address=192.168.0.0/16 comment="defconf: RFC6890" list=not_global_ipv4
add address=198.18.0.0/15 comment="defconf: RFC6890 benchmark" list=not_global_ipv4
add address=255.255.255.255/32 comment="defconf: RFC6890" list=not_global_ipv4

add address=224.0.0.0/4 comment="defconf: multicast" list=bad_src_ipv4
add address=255.255.255.255/32 comment="defconf: RFC6890" list=bad_src_ipv4

add address=0.0.0.0/8 comment="defconf: RFC6890" list=bad_dst_ipv4
add address=224.0.0.0/4 comment="defconf: RFC6890" list=bad_dst_ipv4

add comment="ddosconf: DDoS" list=ddos_targets_ipv4
add comment="ddosconf: DDoS" list=ddos_attackers_ipv4


/ip firewall filter

add action=accept chain=input comment="defconf: accept ICMP after RAW" protocol=icmp
add action=accept chain=input comment="defconf: accept established,related,untracked" connection-state=established,related,untracked
add action=drop chain=input comment="defconf: drop all not coming from LAN" in-interface-list=!LAN
add action=accept chain=forward comment="defconf: accept all that matches IPSec policy" ipsec-policy=in,ipsec disabled=yes

add action=fasttrack-connection chain=forward comment="defconf: fasttrack" connection-state=established,related disabled=yes
add action=accept chain=forward comment="defconf: fasttrack accept established,related" connection-state=established,related disabled=yes

add action=drop chain=forward comment="defconf: drop invalid" connection-state=invalid

add action=drop chain=forward comment="defconf: drop all from WAN not DSTNATed" connection-nat-state=!dstnat connection-state=new in-interface-list=WAN log=yes log-prefix=fw_wan_not_DSTNATed
add action=drop chain=forward comment="modemconf: drop all from MODEM not DSTNATed" connection-nat-state=!dstnat connection-state=new in-interface-list=MODEM log=yes log-prefix=fw_modem_not_DSTNATed

add action=drop chain=forward src-address-list=no_forward_ipv4 comment="defconf: drop bad forward IPs"
add action=drop chain=forward dst-address-list=no_forward_ipv4 comment="defconf: drop bad forward IPs"

add action=jump chain=forward connection-state=new jump-target=detect_ddos comment="ddosconf: DDoS"
add action=return chain=detect_ddos dst-limit=32,32,src-and-dst-addresses/10s protocol=tcp tcp-flags=syn,ack comment="ddosconf: SYN-ACK Flood" log=yes log-prefix=fw_syn_ack_detected
add action=return chain=detect_ddos dst-limit=64,128,src-and-dst-addresses/10s comment="ddosconf: DDoS"
add action=add-dst-to-address-list chain=detect_ddos address-list=ddos_targets_ipv4 address-list-timeout=10m comment="ddosconf: DDoS" log=yes log-prefix=fw_ddos_targets_detected
add action=add-src-to-address-list chain=detect_ddos address-list=ddos_attackers_ipv4 address-list-timeout=10m comment="ddosconf: DDoS" log=yes log-prefix=fw_ddos_attackers_detected


/ip firewall nat

add action=accept chain=srcnat comment="defconf: accept all that matches IPSec policy" ipsec-policy=out,ipsec disabled=yes
add action=masquerade chain=srcnat comment="defconf: masquerade" out-interface-list=WAN

add action=masquerade chain=srcnat out-interface-list=MODEM src-address-list=local_subnet_ipv4 dst-address-list=modem_ipv4 comment="modemconf: Access To Modem"

add action=accept chain=dstnat dst-port=53 in-interface-list="LAN" protocol=udp src-address-list=local_dns_ipv4 comment="lanconf: Accept Local DNS Startup Query(UDP)" log=yes log-prefix=fw_dnsv4_udp
add action=accept chain=dstnat dst-port=53 in-interface-list="LAN" protocol=tcp src-address-list=local_dns_ipv4 comment="lanconf: Accept Local DNS Startup Query(TCP)" log=yes log-prefix=fw_dnsv4_tcp

add action=redirect chain=dstnat dst-address-list=!local_dns_ipv4 dst-port=53 in-interface-list="LAN" protocol=udp to-addresses=172.16.1.1 to-ports=53 comment="lanconf: DNS Redirect to DNS Server(UDP)"
add action=redirect chain=dstnat dst-address-list=!local_dns_ipv4 dst-port=53 in-interface-list="LAN" protocol=tcp to-addresses=172.16.1.1 to-ports=53 comment="lanconf: DNS Redirect to DNS Server(TCP)"


/ip firewall mangle

add action=change-mss chain=forward comment="defconf: Fix IPv4 MSS For PPPoE" new-mss=clamp-to-pmtu passthrough=yes protocol=tcp tcp-flags=syn
add action=accept chain=prerouting src-address-list=local_subnet_ipv4 dst-address-list=modem_ipv4 comment="modemconf: Access To Modem"


/ip firewall raw

add action=accept chain=prerouting comment="defconf: enable for transparent firewall" disabled=yes

add action=drop chain=prerouting dst-address-list=ddos_targets_ipv4 src-address-list=ddos_attackers_ipv4 comment="ddosconf: DDoS"

add action=accept chain=prerouting comment="defconf: accept DHCP discover" dst-address=255.255.255.255 dst-port=67 in-interface-list=LAN protocol=udp src-address=0.0.0.0 src-port=68

add action=drop chain=prerouting comment="defconf: drop bogon IP's" src-address-list=bad_ipv4
add action=drop chain=prerouting comment="defconf: drop bogon IP's" dst-address-list=bad_ipv4
add action=drop chain=prerouting comment="defconf: drop bogon IP's" src-address-list=bad_src_ipv4
add action=drop chain=prerouting comment="defconf: drop bogon IP's" dst-address-list=bad_dst_ipv4

add action=drop chain=prerouting comment="defconf: drop non global from WAN" src-address-list=not_global_ipv4 in-interface-list=WAN
add action=drop chain=prerouting comment="defconf: drop forward to local lan from WAN" in-interface-list=WAN dst-address-list=local_subnet_ipv4 log=yes log-prefix=fw_wan_to_lan_forward

add action=drop chain=prerouting comment="modemconf: drop if not from modem IP" in-interface-list=MODEM src-address-list=!modem_ipv4 log=yes log-prefix=fw_not_from_modem_address
add action=drop chain=prerouting comment="modemconf: drop forward to local lan from MODEM" in-interface-list=MODEM dst-address-list=local_subnet_ipv4 log=yes log-prefix=fw_modem_to_lan_forward

add action=drop chain=prerouting comment="defconf: drop local if not from default IP range" in-interface-list=LAN src-address-list=!local_subnet_ipv4

add action=drop chain=prerouting comment="defconf: drop bad UDP" port=0 protocol=udp

add action=jump chain=prerouting comment="defconf: jump to ICMP chain" jump-target=icmp4 protocol=icmp
add action=jump chain=prerouting comment="defconf: jump to TCP chain" jump-target=bad_tcp protocol=tcp

add action=accept chain=prerouting comment="defconf: accept everything else from LAN" in-interface-list=LAN
add action=accept chain=prerouting comment="defconf: accept everything else from WAN" in-interface-list=WAN
add action=accept chain=prerouting comment="modemconf: accept everything else from MODEM" in-interface-list=MODEM

add action=drop chain=prerouting comment="defconf: drop the rest"

add action=drop chain=bad_tcp comment="defconf: TCP flag filter" protocol=tcp tcp-flags=!fin,!syn,!rst,!ack
add action=drop chain=bad_tcp comment="defconf: tcp-flags=fin,syn" protocol=tcp tcp-flags=fin,syn
add action=drop chain=bad_tcp comment="defconf: tcp-flags=fin,rst" protocol=tcp tcp-flags=fin,rst
add action=drop chain=bad_tcp comment="defconf: tcp-flags=fin,!ack" protocol=tcp tcp-flags=fin,!ack
add action=drop chain=bad_tcp comment="defconf: tcp-flags=fin,urg" protocol=tcp tcp-flags=fin,urg
add action=drop chain=bad_tcp comment="defconf: tcp-flags=syn,rst" protocol=tcp tcp-flags=syn,rst
add action=drop chain=bad_tcp comment="defconf: tcp-flags=rst,urg" protocol=tcp tcp-flags=rst,urg
add action=drop chain=bad_tcp comment="defconf: TCP port 0 drop" port=0 protocol=tcp

add action=accept chain=icmp4 comment="lanconf: Accept echo reply from WAN" icmp-options=0:0 limit=5,10:packet protocol=icmp in-interface-list=WAN
add action=accept chain=icmp4 comment="lanconf: Accept net unreachable from WAN" icmp-options=3:0 protocol=icmp in-interface-list=WAN
add action=accept chain=icmp4 comment="lanconf: Accept fragmentation needed from WAN" icmp-options=3:4 protocol=icmp in-interface-list=WAN
add action=accept chain=icmp4 comment="lanconf: Accept time exceeded from WAN" icmp-options=11:0-255 protocol=icmp in-interface-list=WAN
add action=drop chain=icmp4 comment="lanconf: Drop other icmp from WAN" protocol=icmp in-interface-list=WAN

add action=accept chain=icmp4 comment="modemconf: Accept echo reply from Modem" icmp-options=0:0 limit=5,10:packet protocol=icmp in-interface-list=MODEM
add action=drop chain=icmp4 comment="modemconf: Drop other icmp from Modem" protocol=icmp in-interface-list=MODEM log=yes log-prefix=fw_drop_modem_icmp

add action=accept chain=icmp4 comment="defconf: echo reply" icmp-options=0:0 limit=5,10:packet protocol=icmp in-interface-list=LAN log=yes log-prefix=fw_lan_ehco_reply
add action=accept chain=icmp4 comment="defconf: net unreachable" icmp-options=3:0 protocol=icmp in-interface-list=LAN
add action=accept chain=icmp4 comment="defconf: host unreachable" icmp-options=3:1 protocol=icmp in-interface-list=LAN
add action=accept chain=icmp4 comment="defconf: protocol unreachable" icmp-options=3:2 protocol=icmp in-interface-list=LAN
add action=accept chain=icmp4 comment="defconf: port unreachable" icmp-options=3:3 protocol=icmp in-interface-list=LAN
add action=accept chain=icmp4 comment="defconf: fragmentation needed" icmp-options=3:4 protocol=icmp in-interface-list=LAN

add action=accept chain=icmp4 comment="defconf: echo to Modem" icmp-options=8:0 limit=5,10:packet protocol=icmp in-interface-list=LAN dst-address-list=modem_ipv4
add action=accept chain=icmp4 comment="defconf: echo to Local Device" icmp-options=8:0 limit=5,10:packet protocol=icmp in-interface-list=LAN dst-address-list=local_subnet_ipv4
add action=drop chain=icmp4 comment="defconf: echo to Not Global" icmp-options=8:0 limit=5,10:packet protocol=icmp in-interface-list=LAN dst-address-list=not_global_ipv4
add action=accept chain=icmp4 comment="defconf: echo to WAN" icmp-options=8:0 limit=5,10:packet protocol=icmp in-interface-list=LAN
add action=accept chain=icmp4 comment="defconf: time exceeded" icmp-options=11:0-255 protocol=icmp in-interface-list=LAN
add action=drop chain=icmp4 comment="defconf: drop all other icmp" protocol=icmp


/ip settings
set tcp-syncookies=yes max-neighbor-entries=1024

/ip neighbor discovery-settings
set discover-interface-list=none

/ip proxy
set enabled=no

/ip socks
set enabled=no

/ip upnp
set enabled=no

/ip cloud
set ddns-enabled=no update-time=no

/ip ssh
set strong-crypto=yes

/tool mac-server
set allowed-interface-list=none

/tool mac-server mac-winbox
set allowed-interface-list=none

/tool mac-server ping
set enabled=no

/tool bandwidth-server
set enabled=no
