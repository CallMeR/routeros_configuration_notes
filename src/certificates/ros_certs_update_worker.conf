## Fox RouterOS Certificates Updater
:local LetsencryptURL "https://letsencrypt.org/certs/isrgrootx1.pem";
:local MkcertURL "https://mkcert.org/generate/all/except/nothing";
:local RootCaSafe "isrgrootx1_safe";
:local RootCaUnsafe "isrgrootx1_unsafe";
:local RootCaChecked;
:local GetRootCaID;
:local LenRootCaID;
:local UseCert;
:local RootCaFile;
:local RootCaFileSize;
:local MkcertFetch;
:local MkcertFile;
:local MkcertFileSize;
:local GetCertsExpired;
:local LenCertsExpired;
:local DnsHttps;
:local DnsVerify;

## Remove old certificates with less than 90 days valid
:set GetCertsExpired [/certificate find where expires-after < 90d ];
:set LenCertsExpired [:len $GetCertsExpired ];

:if ($LenCertsExpired > 0) do={
    /certificate remove $GetCertsExpired;
    :set MkcertFetch true;
    :log info ("Removed $LenCertsExpired expired certificates.");
}
## Remove old certificates End

## Check If DNS Resolution is Affected
:set DnsHttps [/ip dns get use-doh-server ];
:set DnsVerify [/ip dns get verify-doh-cert ];

:if ($DnsHttps != "" && $DnsVerify) do={
    /ip dns cache flush

    :do {
        # Attempt to resolve domains
        :resolve letsencrypt.org;
        :resolve mkcert.org;
    } on-error={
        # Temporarily disable DoH certificate verification if DNS resolution fails
        :log info ("DoH cert verification disabled due to validation failure.");
        /ip dns set verify-doh-cert=no
        :delay 3s;
    }

}
## Check DNS End

## Check If Root CA Named 'isrg*' Exists
:set GetRootCaID [/certificate find where name ~ "isrg*" ];
:set LenRootCaID [:len $GetRootCaID];

:if ($LenRootCaID = 0) do={
    :set RootCaFile $RootCaUnsafe;
} else={
    :set GetRootCaID [/certificate find where name=$RootCaUnsafe ];
    :set LenRootCaID [:len $GetRootCaID ];

    :if ($LenRootCaID > 0) do={
        :set UseCert "yes";
        :set RootCaFile $RootCaSafe;
    } else={
        :set UseCert "yes";
        :set RootCaChecked "yes";
        :log info ("Root certificate is up to date.")
    }

}
## Check Root CA End

## Process the Root CA Update
:if ($UseCert = nothing) do={
    :log info ("Root certificate not detected. Updating ...");
    /tool fetch url=$LetsencryptURL check-certificate=no dst-path=$RootCaFile;
    :delay 10s;
    :set RootCaFileSize [/file get $RootCaFile size ];

    # If download is successful (file size > 1000), import the certificate
    :if ($RootCaFileSize > 1000) do={
        /certificate import name=$RootCaFile passphrase="" file-name=$RootCaFile;
        :set RootCaFile $RootCaSafe;
        :delay 3s;
    } else={
        /file remove $RootCaFile;
        :set MkcertFetch false;
        :log info ("Root certificate download failed. Size: $RootCaFileSize. Aborting...");
    }

}

:if ($RootCaChecked = nothing) do={
    :log info ("Unsafe root certificate detected. Updating ...");
    /tool fetch url=$LetsencryptURL check-certificate=yes dst-path=$RootCaFile;
    :delay 10s;
    :set RootCaFileSize [/file get $RootCaFile size ];

    # If download is successful (file size > 1000), import the certificate
    :if ($RootCaFileSize > 1000) do={
        /certificate remove [find where name ~ "isrg*" ];
        /certificate import name=$RootCaFile passphrase="" file-name=$RootCaFile;
        :set MkcertFetch true;
        :delay 3s;
    } else={
        /file remove $RootCaFile;
        :set MkcertFetch false;
        :log info ("Root certificate download failed. Size: $RootCaFileSize. Aborting...");
    }

}
## Process the Root CA Update End

## Process the Mkcert Update
:if ($MkcertFetch) do={
    :log info ("Mkcert certificate requires an update. Updating ...");
    :local Date [/system clock get date ];
    :set MkcertFile "mkcert_$Date";
    /tool fetch url=$MkcertURL check-certificate=yes dst-path=$MkcertFile;
    :delay 10s;
    :set MkcertFileSize [/file get $MkcertFile size ];

    # If download is successful (file size > 200000), import the certificate
    :if ($MkcertFileSize > 200000) do={
        /certificate remove [find where name ~ "mkcert*" ];
        /certificate import name=$MkcertFile passphrase="" file-name=$MkcertFile;
    } else={
        /file remove $MkcertFile;
        :log error ("Mkcert download failed. Size: $MkcertFileSize. Aborting...");
    }

} else={
    :log info ("Mkcert certificate update skipped.")
}
## Process the Mkcert Update End

## Process the DoH Restore
:set DnsHttps [/ip dns get use-doh-server ];
:set DnsVerify [/ip dns get verify-doh-cert ];

:if ($DnsHttps != "" && !$DnsVerify) do={
    # Re-enable DoH certificate verification
    /ip dns set verify-doh-cert=yes
    /ip dns cache flush
    :delay 3s;

    :do {
        # Attempt to resolve domains
        :resolve letsencrypt.org;
        :resolve mkcert.org;
        :log info ("DoH cert verification re-enabled.");
    } on-error={
        # If DNS resolution fails, disable DoH certificate verification
        /ip dns set verify-doh-cert=no
        :log info ("DoH cert verification disabled due to validation failure.");
    }

}
## Process the DoH Restore End

## Fox RouterOS Certificates Updater End

